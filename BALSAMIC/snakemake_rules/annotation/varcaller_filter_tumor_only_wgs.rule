# vim: syntax=python tabstop=4 expandtab
# coding: utf-8
# NGS filters for various scenarios


rule bcftools_filter_tnscope_research_tumor_only:
  input:
    vcf_snv_research = vep_dir + "{var_type}.somatic.{case_name}.tnscope.research.vcf.gz",
    wgs_calling_file = config["reference"]["wgs_calling_regions"]
  output:
    vcf_pass_tnscope = vep_dir + "{var_type}.somatic.{case_name}.tnscope.research.filtered.pass.vcf.gz",
    bcftools_counts_research = vep_dir + "{var_type}.somatic.{case_name}.tnscope.research.filtered.pass.stats"
  benchmark:
    Path(benchmark_dir, 'bcftools_filter_tnscope_research_tumor_only_' + "{var_type}.somatic.{case_name}.tsv").as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("bcftools") + ".sif").as_posix()
  params:
    pop_freq = get_tag_and_filtername(snv_research_filters, "balsamic_high_pop_freq"),
    swegen_freq = get_tag_and_filtername(snv_research_filters, "SWEGENAF"),
    hard_filters = WgsSNVFilters.get_bcftools_filter_string(category="research", analysis_type=analysis_type, soft_filter_normals=soft_filter_normal),
    case_name = '{case_name}'
  message:
    "Filtering WGS tumor-only tnscope annotated research variants using bcftools for {params.case_name}"
  shell:
    """
grep -v '^@' {input.wgs_calling_file} > {input.wgs_calling_file}.bed

bcftools view --threads {resources.threads} --regions-file {input.wgs_calling_file}.bed {input.vcf_snv_research} | \
bcftools filter --threads {resources.threads} --include 'INFO/GNOMADAF_popmax <= {params.pop_freq[0]} || INFO/GNOMADAF_popmax == \".\"' --soft-filter '{params.pop_freq[1]}' --mode '+' | \
bcftools filter --threads {resources.threads} --include 'INFO/SWEGENAF <= {params.swegen_freq[0]} || INFO/SWEGENAF == \".\"' --soft-filter '{params.swegen_freq[1]}' --mode '+' |\
bcftools view --threads {resources.threads} --exclude '{params.hard_filters}' -o {output.vcf_pass_tnscope} -O z;

tabix -p vcf -f {output.vcf_pass_tnscope};

bcftools +counts {output.vcf_pass_tnscope} > {output.bcftools_counts_research};
    """

rule bcftools_filter_tnscope_clinical_tumor_only:
  input:
    vcf_snv_clinical = vep_dir + "{var_type}.somatic.{case_name}.tnscope.clinical.vcf.gz",
    wgs_calling_file = config["reference"]["wgs_calling_regions"],
    namemap = vep_dir + "status_to_sample_id_namemap"
  output:
    vcf_pass_tnscope = vep_dir + "{var_type}.somatic.{case_name}.tnscope.clinical.filtered.pass.vcf.gz",
    bcftools_counts_clinical = vep_dir + "{var_type}.somatic.{case_name}.tnscope.clinical.filtered.pass.stats"
  benchmark:
    Path(benchmark_dir, 'bcftools_filter_tnscope_clinical_tumor_only_' + "{var_type}.somatic.{case_name}.tsv").as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("bcftools") + ".sif").as_posix()
  params:
    artefact_freq = get_tag_and_filtername(snv_clinical_filters, "ArtefactFrq"),
    pop_freq = get_tag_and_filtername(snv_research_filters, "balsamic_high_pop_freq"),
    swegen_freq = get_tag_and_filtername(snv_research_filters, "SWEGENAF"),
    loqusdb_clinical_freq = get_tag_and_filtername(snv_clinical_filters, "Frq"),
    hard_filters = WgsSNVFilters.get_bcftools_filter_string(category="clinical", analysis_type=analysis_type, soft_filter_normals=soft_filter_normal),
    housekeeper_id= {"id": config["analysis"]["case_id"],"tags": "clinical"},
    whitelist_script= get_script_path("whitelist_snvs.py"),
    whitelist_snvs= config_model.analysis.whitelist_snvs,
    tmpdir= tempfile.mkdtemp(prefix=tmp_dir),
    case_name = '{case_name}'
  message:
    "Filtering WGS tumor-only tnscope annotated clinical variants using bcftools for {params.case_name}"
  shell:
    """
grep -v '^@' {input.wgs_calling_file} > {input.wgs_calling_file}.bed;

bcftools view --regions-file {input.wgs_calling_file}.bed {input.vcf_snv_clinical} | \
bcftools reheader --threads {resources.threads} -s {input.namemap} | \
bcftools filter --threads {resources.threads} --include 'INFO/ArtefactFrq <= {params.artefact_freq[0]} || INFO/ArtefactFrq == \".\"' --soft-filter '{params.artefact_freq[1]}' --mode '+' |\
bcftools filter --threads {resources.threads} --include 'INFO/GNOMADAF_popmax <= {params.pop_freq[0]} || INFO/GNOMADAF_popmax == \".\"' --soft-filter '{params.pop_freq[1]}' --mode '+' | \
bcftools filter --threads {resources.threads} --include 'INFO/SWEGENAF <= {params.swegen_freq[0]} || INFO/SWEGENAF == \".\"' --soft-filter '{params.swegen_freq[1]}' --mode '+' | \
bcftools filter --threads {resources.threads} --include 'INFO/Frq <= {params.loqusdb_clinical_freq[0]} || INFO/Frq == \".\"' --soft-filter '{params.loqusdb_clinical_freq[1]}' --mode '+' | \
bcftools view --threads {resources.threads} --exclude '{params.hard_filters}' -o {params.tmpdir}/clinical.filtered.pass_before_whitelisted.vcf.gz ;

if [[ -f "{params.whitelist_snvs}" ]]; then
  python {params.whitelist_script} {params.tmpdir}/clinical.filtered.pass_before_whitelisted.vcf.gz --whitelist-path {params.whitelist_snvs} {params.tmpdir}/clinical.filtered.pass_after_whitelisted.vcf ;
else:
  python {params.whitelist_script} {params.tmpdir}/clinical.filtered.pass_before_whitelisted.vcf.gz {params.tmpdir}/clinical.filtered.pass_after_whitelisted.vcf ;
fi

bgzip {params.tmpdir}/clinical.filtered.pass_after_whitelisted.vcf ;
mv {params.tmpdir}/clinical.filtered.pass_after_whitelisted.vcf.gz {output.vcf_pass_tnscope} ;

tabix -p vcf -f {output.vcf_pass_tnscope};

bcftools +counts {output.vcf_pass_tnscope} > {output.bcftools_counts_clinical};
    """


