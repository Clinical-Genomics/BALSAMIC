# vim: syntax=python tabstop=4 expandtab
# coding: utf-8
# NGS filters for various scenarios



rule bcftools_filter_tnscope_tumor_normal:
  input:
    vcf = vep_dir + "{var_type}.somatic.{case_name}.tnscope.research.vcf.gz",
  output:
    vcf_research_pass_tnscope = vep_dir + "{var_type}.somatic.{case_name}.tnscope.research.filtered.pass.vcf.gz",
    bcftools_counts = vep_dir + "{var_type}.somatic.{case_name}.tnscope.research.filtered.pass.stats"
  benchmark:
    Path(benchmark_dir, 'bcftools_filter_tnscope_tumor_normal_' + "{var_type}.somatic.{case_name}.tsv").as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("bcftools") + ".sif").as_posix()
  params:
    pop_freq = [SENTIEON_CALLER.pop_freq.tag_value, SENTIEON_CALLER.pop_freq.filter_name],
    housekeeper_id = {"id": config["analysis"]["case_id"], "tags": "research"},
    case_name = '{case_name}'
  threads:
    get_threads(cluster_config, 'bcftools_filter_tnscope_tumor_normal')
  message:
    "Filtering WGS tumor-normal tnscope annotated variants using bcftools for {params.case_name}"
  shell:
    """
bcftools view {input.vcf} \
| bcftools filter --threads {threads} --include 'INFO/GNOMADAF_popmax <= {params.pop_freq[0]} || INFO/GNOMADAF_popmax == \".\"' --soft-filter '{params.pop_freq[1]}' --mode '+' \
| bcftools view -f PASS -o {output.vcf_research_pass_tnscope} -O z;

tabix -p vcf -f {output.vcf_research_pass_tnscope};

bcftools +counts {output.vcf_research_pass_tnscope} > {output.bcftools_counts};
    """
