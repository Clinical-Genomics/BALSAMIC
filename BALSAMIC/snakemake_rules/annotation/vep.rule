# vim: syntax=python tabstop=4 expandtab
# coding: utf-8
# VEP annotation module. Annotate all VCFs generated through VEP
# This produces both vcf.gz and tab delimited output.
# NOTE: Tab delimited output contains only PASSed variants

from BALSAMIC.utils.rule import get_conda_env
from BALSAMIC.utils.rule import get_threads

rule vep_somatic:
  input:
    vcf = vcf_dir + "{type}.somatic.{sample}.{caller}.vcf.gz",
    header = vcf_dir + "{type}.somatic.{sample}.{caller}.sample_name_map",
    cosmic = config["reference"]["cosmic"]
  output:
    vcf = vep_dir + "{type}.somatic.{sample}.{caller}.vcf.gz",
    vcf_pass = vep_dir + "{type}.somatic.{sample}.{caller}.pass.vcf.gz",
  params:
    conda = get_conda_env(config["conda_env_yaml"],"ensembl-vep"),
    vep_cache = config["reference"]["vep"]
  threads: get_threads(cluster_config, 'vep')
  singularity: singularity_image 
  benchmark:
    benchmark_dir + 'vep_' + "{type}.somatic.{sample}.{caller}.vep.tsv"
  shell:
    "source activate {params.conda}; "
    "vep_path=$(dirname $(readlink -e $(which vep))); "
    "export PERL5LIB=;"
    "vep "
        "--dir $vep_path "
        "--dir_cache {params.vep_cache} "
        "--dir_plugins $vep_path "
        "--input_file {input.vcf} "
        "--output_file {output.vcf} "
        "--compress_output bgzip "
        "--fork {threads} "
        "--vcf "
        "--everything "
        "--allow_non_variant "
        "--dont_skip "
        "--buffer_size 10000 "
        "--format vcf "
        "--offline "
        "--variant_class "
        "--merged "
        "--cache "
        "--custom {input.cosmic},COSMIC,vcf,exact,0,CDS,GENE,STRAND,CNT,AA "
        "--verbose "
        "--force_overwrite; "
    "tabix -p vcf -f {output.vcf}; "
    "bcftools view -f PASS -o {output.vcf_pass} -O z {output.vcf}; "
    "tabix -p vcf -f {output.vcf_pass}; " 
    

rule vep_stat:
  input:
    vep = vep_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".vcfmerge.vcf.gz",
  output:
    stat = vep_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".vcfmerge.balsamic_stat"
  params:
    bed = config["panel"]["capture_kit"] if "panel" in config else "",
    conda = get_conda_env(config["conda_env_yaml"],"ensembl-vep"),
    filter_vep_string = "'not COSMIC and not Existing_variation and synonymous_variant'", 
    bcftools_filter_string = "INFO/DP>=50 && FORMAT/AD>=5 && FORMAT/AF>=0.1",
  threads: get_threads(cluster_config, 'vep')
  singularity: singularity_image 
  benchmark:
    benchmark_dir + 'vep_stat_' + "SNV.somatic." + config["analysis"]["case_id"] + ".vcfmerge.balsamic_stat.tsv" 
  shell:
    "source activate {params.conda}; "
    "if [ \"{params.bed}\" == \"\" ]; then region_size=3101.78817; else region_size=$(awk '{{s+=$3-$2}}END{{print s/1e6}}' {params.bed}); fi; "
    "bcftools view -f PASS {input.vep} 2> /dev/null "
        " | bcftools filter -i \"{params.bcftools_filter_string}\" "
        " | filter_vep --ontology --filter {params.filter_vep_string} "
        " | bcftools +counts "
        " | awk -v region=${{region_size}} '$0~/SNPs/ {{print \"tmb:\",$NF/region}}' > {output.stat};"
    
