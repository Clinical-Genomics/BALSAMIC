# vim: syntax=python tabstop=4 expandtab
# coding: utf-8
# VEP annotation module. Annotate all VCFs generated through VEP


rule vep_somatic_snv:
  input:
    vcf = vcf_dir + "SNV.somatic.{case_name}.{var_caller}.vcf.gz",
    header = vcf_dir + "SNV.somatic.{case_name}.{var_caller}.sample_name_map",
    cosmic = config["reference"]["cosmic"]
  output:
    vcf_all = temp(vep_dir + "SNV.somatic.{case_name}.{var_caller}.all.vcf.gz"),
  benchmark:
    Path(benchmark_dir, "vep_somatic_SNV.somatic.{case_name}.{var_caller}.tsv").as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("ensembl-vep") + ".sif").as_posix()
  params:
    housekeeper_id = {"id": "{case_name}", "tags": "annotated-somatic"},
    ref_path = Path(config["reference"]["gnomad_variant"]).parent.as_posix(),
    message_text = "SNV.somatic.{case_name}.{var_caller}.vcf.gz",
    tmpvcf = vep_dir + "SNV.somatic.{case_name}.{var_caller}.tmp.vcf.gz",
    vcfanno_toml = VCFANNO_TOML,
    vep_cache = config["reference"]["vep"],
    vep_defaults = params.vep.vep_filters
  threads:
    get_threads(cluster_config, "vep_somatic_snv")
  message:
    "Running vep annotation on {params.message_text}"
  shell:
    """
vep_path=$(dirname $(readlink -f $(which vep)));
tmpvcf={params.tmpvcf};
export PERL5LIB=;

vcfanno -p {threads} --base-path {params.ref_path} {params.vcfanno_toml} {input.vcf} \
| bcftools reheader --threads {threads} -s {input.header} \
| bcftools view --threads {threads} -O z -o $tmpvcf ;

vep \
--dir $vep_path \
--dir_cache {params.vep_cache} \
--dir_plugins $vep_path \
--input_file $tmpvcf \
--output_file {output.vcf_all} \
--fork {threads} \
{params.vep_defaults} \
--custom {input.cosmic},COSMIC,vcf,exact,0,CDS,GENE,STRAND,CNT,AA ;

tabix -p vcf -f {output.vcf_all};

rm $tmpvcf;
    """

rule vep_somatic_sv:
  input:
    vcf = vcf_dir + "SV.somatic.{case_name}.svdb.vcf.gz",
    header = vcf_dir + "SV.somatic.{case_name}.svdb.sample_name_map",
  output:
    vcf_all = temp(vep_dir + "SV.somatic.{case_name}.svdb.all.vcf.gz"),
  benchmark:
    Path(benchmark_dir, "vep_somatic_SV.somatic.{case_name}.svdb.tsv").as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("ensembl-vep") + ".sif").as_posix()
  params:
    housekeeper_id = {"id": "{case_name}", "tags": "annotated-somatic"},
    message_text = "SV.somatic.{case_name}.svdb.vcf.gz",
    vep_cache = config["reference"]["vep"],
    vep_defaults = params.vep.vep_filters
  threads:
    get_threads(cluster_config, "vep_somatic_sv")
  message:
    "Running vep annotation on {params.message_text}"
  shell:
    """
vep_path=$(dirname $(readlink -f $(which vep)));
export PERL5LIB=;

bcftools reheader --threads {threads} -s {input.header} {input.vcf} | \
bcftools view --threads {threads} -O v | \
vep \
--dir $vep_path \
--dir_cache {params.vep_cache} \
--dir_plugins $vep_path \
--output_file {output.vcf_all} \
--fork {threads} \
{params.vep_defaults} \

tabix -p vcf -f {output.vcf_all};
    """

rule tmb_calculation:
  input:
    vep = vep_dir + "{var_type}.somatic.{case_name}.{var_caller}.all.vcf.gz",
  output:
    tmb = vep_dir + "{var_type}.somatic.{case_name}.{var_caller}.balsamic_stat"
  benchmark:
    Path(benchmark_dir, "vep_stat_{var_type}.somatic.{case_name}.{var_caller}.tsv").as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("ensembl-vep") + ".sif").as_posix() 
  params:
    af_cutoff = "0.05",
    bed = config["panel"]["capture_kit"] if "panel" in config else "",
    housekeeper_id = {"id": "{case_name}", "tags": "stat-somatic"},
    message_text = "{var_type}.somatic.{case_name}.{var_caller}.all",
    tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
  threads:
    get_threads(cluster_config, "vep")
  message:
    "Calculating TMB for {params.message_text}" 
  shell:
    """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};

if [ \"{params.bed}\" == \"\" ]; then region_size=3101.78817; else region_size=$(awk '{{s+=$3-$2}}END{{print s/1e6}}' {params.bed}); fi;

echo -e '##INFO=<ID=AF_TUMOR,Number=1,Type=Float,Description="Allele Frequency of Tumor Sample">' > {params.tmpdir}/vcf_header

bcftools query -s TUMOR \
-f '%CHROM\\t%POS\\t[%AF]\\n' \
{input.vep} \
| bgzip -c > {params.tmpdir}/tumor.txt.gz;

tabix -f -s1 -b2 -e2 {params.tmpdir}/tumor.txt.gz;

bcftools view -s TUMOR {input.vep} \
| bcftools annotate -s TUMOR \
-a {params.tmpdir}/tumor.txt.gz \
-h {params.tmpdir}/vcf_header \
-c CHROM,POS,INFO/AF_TUMOR \
-O z -o {params.tmpdir}/temp.vcf.gz;

tabix -f -p vcf {params.tmpdir}/temp.vcf.gz;

bcftools view --types snps,indels --apply-filters PASS {params.tmpdir}/temp.vcf.gz \
| bcftools filter -i "INFO/AF_TUMOR>={params.af_cutoff}" \
| filter_vep --filter 'not Existing_variation' \
| filter_vep --filter 'not COSMIC' \
| filter_vep --filter 'not non_coding_transcript_exon_variant' \
| filter_vep --filter 'not non_coding_transcript_variant' \
| filter_vep --filter 'not feature_truncation' -C \
| awk -v region=${{region_size}} '{{print $NF/region}}' > {output.tmb};

rm -r {params.tmpdir};
    """


rule vep_germline:
  input:
    vcf = vcf_dir + "{var_type}.germline.{sample}.{var_caller}.vcf.gz",
    cosmic = config["reference"]["cosmic"]
  output:
    vcf_all = vep_dir + "{var_type}.germline.{sample}.{var_caller}.vcf.gz",
  benchmark:
    Path(benchmark_dir, "vep_germline_{var_type}.germline.{sample}.{var_caller}.tsv").as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("ensembl-vep") + ".sif").as_posix() 
  params:
    housekeeper_id = {"id": "{sample}", "tags": "annotated-germline"},
    sample = '{sample}',
    vep_cache = config["reference"]["vep"],
    vep_defaults = params.vep.vep_filters
  threads:
    get_threads(cluster_config, 'vep_germline')
  message:
    "Running vep annotation on {params.sample}"
  shell:
      """
vep_path=$(dirname $(readlink -f $(which vep)));
export PERL5LIB=;

vep \
--dir $vep_path \
--dir_cache {params.vep_cache} \
--dir_plugins $vep_path \
--input_file {input.vcf} \
--output_file {output.vcf_all} \
--fork {threads} \
{params.vep_defaults} \
--custom {input.cosmic},COSMIC,vcf,exact,0,CDS,GENE,STRAND,CNT,AA;

tabix -p vcf -f {output.vcf_all};

    """
