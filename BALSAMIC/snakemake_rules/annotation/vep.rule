# vim: syntax=python tabstop=4 expandtab
# coding: utf-8
# VEP annotation module. Annotate all VCFs generated through VEP
# This produces both vcf.gz and tab delimited output.
# NOTE: Tab delimited output contains only PASSed variants

from BALSAMIC.utils.rule import get_conda_env
from BALSAMIC.utils.rule import get_threads

rule vep_somatic:
  input:
    vcf = vcf_dir + "{var_type}.somatic.{case_name}.{var_caller}.vcf.gz",
    header = vcf_dir + "{var_type}.somatic.{case_name}.{var_caller}.sample_name_map",
    cosmic = config["reference"]["cosmic"]
  output:
    vcf = vep_dir + "{var_type}.somatic.{case_name}.{var_caller}.vcf.gz",
    vcf_summary = vep_dir + "{var_type}.somatic.{case_name}.{var_caller}.vcf.gz_summary.html",
    vcf_pass = vep_dir + "{var_type}.somatic.{case_name}.{var_caller}.pass.vcf.gz",
  params:
    tmpvcf = vep_dir + "{var_type}.somatic.{case_name}.{var_caller}.tmp.vcf.gz",
    conda = get_conda_env(config["conda_env_yaml"],"ensembl-vep"),
    vep_cache = config["reference"]["vep"]
  threads: get_threads(cluster_config, 'vep')
  singularity: singularity_image 
  benchmark:
    benchmark_dir + 'vep_' + "{var_type}.somatic.{case_name}.{var_caller}.vep.tsv"
  shell:
    "source activate {params.conda}; "
    "vep_path=$(dirname $(readlink -e $(which vep))); "
    "tmpvcf={params.tmpvcf}; "
    "export PERL5LIB=;"
    "bcftools reheader -s {input.header} {input.vcf} | bcftools view -O z -o $tmpvcf ;" 
    "vep "
        "--dir $vep_path "
        "--dir_cache {params.vep_cache} "
        "--dir_plugins $vep_path "
        "--input_file $tmpvcf "
        "--output_file {output.vcf} "
        "--compress_output bgzip "
        "--fork {threads} "
        "--vcf "
        "--everything "
        "--allow_non_variant "
        "--dont_skip "
        "--buffer_size 10000 "
        "--format vcf "
        "--offline "
        "--variant_class "
        "--merged "
        "--cache "
        "--custom {input.cosmic},COSMIC,vcf,exact,0,CDS,GENE,STRAND,CNT,AA "
        "--verbose "
        "--force_overwrite; "
    "tabix -p vcf -f {output.vcf}; "
    "bcftools view -f PASS -o {output.vcf_pass} -O z {output.vcf}; "
    "tabix -p vcf -f {output.vcf_pass}; "
    "rm $tmpvcf " 
    

rule vep_stat:
  input:
    vep = vep_dir + "SNV.somatic.{case_name}.{var_caller}.pass.vcf.gz",
  output:
    stat = vep_dir + "SNV.somatic.{case_name}.{var_caller}.pass.balsamic_stat"
  params:
    bed = config["panel"]["capture_kit"] if "panel" in config else "",
    conda = get_conda_env(config["conda_env_yaml"],"ensembl-vep"),
    filter_vep_string = "'not COSMIC and not Existing_variation and synonymous_variant'", 
    bcftools_filter_string = "INFO/DP>=50 && FORMAT/AD>=5 && FORMAT/AF>=0.1",
  threads: get_threads(cluster_config, 'vep')
  singularity: singularity_image 
  benchmark:
    benchmark_dir + "vep_stat_SNV.somatic.{case_name}.{var_caller}.pass.balsamic_stat.tsv" 
  shell:
    "source activate {params.conda}; "
    "if [ \"{params.bed}\" == \"\" ]; then region_size=3101.78817; else region_size=$(awk '{{s+=$3-$2}}END{{print s/1e6}}' {params.bed}); fi; "
    "bcftools view --type snps -f PASS {input.vep} 2> /dev/null "
        " | bcftools filter -i \"{params.bcftools_filter_string}\" "
        " | filter_vep --ontology --filter {params.filter_vep_string} "
        " | bcftools +counts "
        " | awk -v region=${{region_size}} '$0~/SNPs/ {{print \"tmb:\",$NF/region}}' > {output.stat};"
    

rule vep_germline:
  input:
    vcf = vcf_dir + "{var_type}.germline.{sample}.{var_caller}.vcf.gz",
    cosmic = config["reference"]["cosmic"]
  output:
    vcf = vep_dir + "{var_type}.germline.{sample}.{var_caller}.vcf.gz",
    vcf_summary = vep_dir + "{var_type}.germline.{sample}.{var_caller}.vcf.gz_summary.html",
  params:
    conda = get_conda_env(config["conda_env_yaml"],"ensembl-vep"),
    vep_cache = config["reference"]["vep"]
  threads: get_threads(cluster_config, 'vep')
  singularity: singularity_image 
  benchmark:
    benchmark_dir + 'vep_' + "{var_type}.germline.{sample}.{var_caller}.vep.tsv"
  shell:
    "source activate {params.conda}; "
    "vep_path=$(dirname $(readlink -e $(which vep))); "
    "export PERL5LIB=;"
    "vep "
        "--dir $vep_path "
        "--dir_cache {params.vep_cache} "
        "--dir_plugins $vep_path "
        "--input_file {input.vcf} "
        "--output_file {output.vcf} "
        "--compress_output bgzip "
        "--fork {threads} "
        "--vcf "
        "--everything "
        "--allow_non_variant "
        "--dont_skip "
        "--buffer_size 10000 "
        "--format vcf "
        "--offline "
        "--variant_class "
        "--merged "
        "--cache "
        "--custom {input.cosmic},COSMIC,vcf,exact,0,CDS,GENE,STRAND,CNT,AA "
        "--verbose "
        "--force_overwrite; "
    "tabix -p vcf -f {output.vcf}; "
