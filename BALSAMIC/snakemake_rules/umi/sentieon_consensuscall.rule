# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

# UMI-consensus calling
rule sentieon_consensuscall_umi:
    input:
        sam_consensus = umi_dir + "{case_name}.umialign.bam"
    output:
        fastq_consensus = temp(umi_dir + "{case_name}.consensuscall.fastq.gz")
    benchmark:
        Path(benchmark_dir + "sentieon_consensuscall_umi_{case_name}.tsv").as_posix()
    params:
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        tag = paramsumi.consensuscall.tag, 
        ip_format = paramsumi.consensuscall.align_format,
        sample_id = '{case_name}'
    threads: 
        get_threads(cluster_config, "sentieon_consensuscall_umi")
    message: 
        "Consensus molecule creation using sentieon for sample {params.sample_id}"
    shell:
        """
{params.sentieon_exec} umi consensus \
-t {threads} \
-i {input.sam_consensus} \
-o {output.fastq_consensus} \
--input_format {params.ip_format} \
--umi_tag {params.tag} \
--read_name_prefix 'UMI-'; 
        """

# Alignment of consensus reads
rule sentieon_bwa_umiconsensus:
    input:
        ref_fa = config["reference"]["reference_genome"],
        fq_consensus = umi_dir + "{case_name}.consensuscall.fastq.gz"
    output:
        align_consensus = umi_dir + "{case_name}.consensusaligned.bam"
    benchmark:
        Path(benchmark_dir + "sentieon_bwa_umiconsensus_{case_name}.tsv").as_posix()
    params:
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        sheader = paramsumi.common.align_header,
        ip_bases = paramsumi.common.align_intbases,
        sample_id = "{case_name}"
    threads: 
        get_threads(cluster_config, "sentieon_bwa_umiconsensus")
    message:
         "Mapping of consensus reads with the sentieon bwa mem, sorting for sample {params.sample_id}"
    shell:
        """
{params.sentieon_exec} bwa mem \
-R {params.sheader} \
-t {threads} \
-K {params.ip_bases} \
-p -C {input.ref_fa} \
{input.fq_consensus} | \
{params.sentieon_exec} util sort \
-r {input.ref_fa} \
--sam2bam \
-o {output.align_consensus} \
-i - ;
        """

# Filter consensus called reads based on 'XZ' filtering
rule sentieon_consensusfilter_umi:
    input:
        umi_dir + "{case_name}.consensusaligned.bam"
    output:
        umi_dir + "{case_name}.consensusfiltered.bam"
    benchmark:
        Path(benchmark_dir + "sentieon_consensusfilter_umi_{case_name}.tsv").as_posix()
    singularity:
        singularity_image
    params:
        consensusfilter_script = get_script_path("FilterDuplexUMIconsensus.awk"),
        minreads = paramsumi.consensuscall.filter_minreads,
        sample_id = '{case_name}',
        conda = get_conda_env(config["conda_env_yaml"],"samtools")
    threads:
        get_threads(cluster_config, "sentieon_consensusfilter_umi")
    message:
        "Filtering consensus reads based on XZ tag for sample {params.sample_id}"
    shell:
        """
source activate {params.conda};

samtools view -h {input} | \
awk -v MinR={params.minreads} -v OFS=\'\\t\' -f {params.consensusfilter_script} | \
samtools view -bh - > {output};

samtools index {output};
        """
