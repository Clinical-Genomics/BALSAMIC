# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

# Generate tables for AF scatterplots
rule bcftools_query_generatebackgroundaf_umitable:
    input:
        vcf = vcf_dir + "SNV.somatic.{case_name}.{var_caller}.vcf.gz"
    output:
        AF = umi_qc_dir + "{case_name}.{var_caller}.AFtable.txt"
    benchmark:
        Path(benchmark_dir, "bcftools_query_generatebackgroundaf_umitable_{case_name}_{var_caller}.tsv").as_posix()
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("bcftools") + ".sif").as_posix()
    params:
        validated_set= config["background_variants"],
        conda = config["bioinfo_tools"].get("bcftools"),
        case_name = "{case_name}"
    threads: 
        get_threads(cluster_config, "bcftools_query_generatebackgroundaf_umitable")
    message: 
        "Creating Allelic frequency table from VCF file for sample {params.case_name}"
    shell:
        """
source activate {params.conda};

bcftools query \
--regions-file {params.validated_set} \
-f \"%CHROM\\t%POS\\t%REF\\t%ALT\\t%FILTER\\t[%AF\\t]\n\" \
{input.vcf} | \
awk -v file={params.case_name} \
\'{{print $1\":\"$2\"_\"$3\"->\"$4\"\\t\"$5\"\\t\"$6\"\\t\"file}}\' \
> {output.AF};
        """
