# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

# Variant-calling using TNscope
rule sentieon_tnscope_umi:
    input:
        bam = umi_dir + "{case_name}.{step}.bam",
        ref_fa = config["reference"]["reference_genome"],
        bed = config["panel"]["capture_kit"],
        dbsnp = config["reference"]["dbsnp"]
    output:
        vcf = vcf_dir +  "SNV.somatic.{case_name}.{step}.TNscope.umi.vcf.gz",
        namemap = vcf_dir + "SNV.somatic.{case_name}.{step}.TNscope.umi.sample_name_map"
    benchmark:
        Path(benchmark_dir + "sentieon_tnscope_{case_name}.{step}.tsv").as_posix()
    params:
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        tumor_af = paramsumi.common.filter_tumor_af,
        algo = paramsumi.tnscope.algo,
        disable_detect = paramsumi.tnscope.disable_detect,
        tumor_lod = config["tumorlod"], #paramsumi.tnscope.min_tumorLOD,
        error_rate = paramsumi.tnscope.error_rate,
        prune_factor = paramsumi.tnscope.prunefactor,
        tumor = get_sample_type(config["samples"], "tumor"),
        sample_id = '{case_name}'
    threads: 
        get_threads(cluster_config, "sentieon_tnscope_umi")
    message: 
        "Calling SNVs using TNscope for sample {params.sample_id}"
    shell:
        """
{params.sentieon_exec} driver \
-t {threads} \
-r {input.ref_fa} \
-i {input.bam} \
--algo {params.algo} \
--tumor_sample {params.sample_id} \
--dbsnp {input.dbsnp} \
--min_tumor_allele_frac {params.tumor_af} \
--filter_t_alt_frac {params.tumor_af} \
--min_init_tumor_lod {params.tumor_lod} \
--disable_detector {params.disable_detect} \
--max_error_per_read {params.error_rate} \
--pcr_indel_model NONE \
--prune_factor {params.prune_factor} \
{output.vcf};

echo -e \"{params.tumor}\\tTUMOR\" > {output.namemap}; 
        """
