# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

# Variant-calling using TNscope



rule sentieon_tnscope_umi:
    input:
        bam = umi_dir + "tumor_umi_consensusfiltered.merged.bam",
        ref_fa = config["reference"]["reference_genome"],
        bed = config["panel"]["capture_kit"],
        dbsnp = config["reference"]["dbsnp"]
    output:
        vcf_tnscope_umi = vcf_dir +  "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope_umi.vcf.gz",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope_umi.sample_name_map"
    benchmark:
        Path(benchmark_dir, "sentieon_tnscope_umi_" + config["analysis"]["case_id"] + ".tsv").as_posix()
    params:
        housekeeper_id = {"id": config["analysis"]["case_id"], "tags": "research"},
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        tumor_af = params.tnscope_umi.filter_tumor_af,
        algo = params.tnscope_umi.algo,
        disable_detect = params.tnscope_umi.disable_detect,
        tumor_lod = params.tnscope_umi.min_tumorLOD,
        init_tumor_lod = params.tnscope_umi.init_tumorLOD,
        error_rate = params.tnscope_umi.error_rate,
        prune_factor = params.tnscope_umi.prunefactor,
        padding = params.tnscope_umi.padding,
        tumor = "TUMOR",
        pcr_model = params.tnscope_umi.pcr_model
    threads:
        get_threads(cluster_config, "sentieon_tnscope_umi")
    message:
        "Calling single nucleotide variants using TNscope for {params.tumor}"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-t {threads} \
-r {input.ref_fa} \
-i {input.bam} \
--interval {input.bed} \
--interval_padding {params.padding} \
--algo {params.algo} \
--tumor_sample {params.tumor} \
--dbsnp {input.dbsnp} \
--min_tumor_allele_frac {params.tumor_af} \
--filter_t_alt_frac {params.tumor_af} \
--min_init_tumor_lod {params.init_tumor_lod} \
--min_tumor_lod {params.tumor_lod} \
--disable_detector {params.disable_detect} \
--max_error_per_read {params.error_rate} \
--pcr_indel_model {params.pcr_model} \
--prune_factor {params.prune_factor} \
{output.vcf_tnscope_umi};

echo -e \"{params.tumor}\\tTUMOR\" > {output.namemap}; 
        """
