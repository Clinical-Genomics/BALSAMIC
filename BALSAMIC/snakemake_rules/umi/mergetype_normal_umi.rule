# vim: syntax=python tabstop=4 expandtab
# coding: utf-8



rule mergeBam_normal_umiconsensus:
  input:
    fasta = config["reference"]["reference_genome"],
    bam = expand(umi_dir + "{mysample}_consensusfiltered_umi.bam", mysample=normal_sample)
  output:
    bam = umi_dir + "normal_umi_consensusfiltered.merged.bam",
    cram = umi_dir + "normal_umi_consensusfiltered.merged.cram"
  benchmark:
    Path(benchmark_dir, 'mergeBam_normal_umiconsensus_' + "{mysample}.tsv".format(mysample=normal_sample)).as_posix()
  singularity:
    Path(singularity_image, config["bioinfo_tools"].get("picard") + ".sif").as_posix()
  params:
    picard = params.common.picard_RG_normal,
    sample = normal_sample,
    housekeeper_id = {"id": normal_sample, "tags": "umi_normal"},
  threads:
    get_threads(cluster_config, "mergeBam_normal_umiconsensus")
  message:
    ("Replace ReadGroups using picard for normal sample {params.sample} "
    "and convert bam to cram format")
  shell:
        """
picard AddOrReplaceReadGroups {params.picard} INPUT={input.bam} OUTPUT={output.bam}; 
samtools index {output.bam};
samtools view -h -T {input.fasta} --threads {threads} -C -o {output.cram} {output.bam}; 
samtools index {output.cram}; 
        """
