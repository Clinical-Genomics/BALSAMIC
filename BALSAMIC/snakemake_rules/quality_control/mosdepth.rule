# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

from BALSAMIC.utils.rule import get_conda_env, get_picard_mrkdup

picarddup = get_picard_mrkdup(config)

rule mosdepth_coverage:
  input:
    bam = bam_dir + "{sample}" + ".sorted." + picarddup + ".bam",
    bed = config["panel"]["capture_kit"]
  output:
    bam_dir + "{sample}.mosdepth.global.dist.txt",
    bam_dir + "{sample}.mosdepth.region.dist.txt",
    bam_dir + "{sample}.mosdepth.summary.txt",
    bam_dir + "{sample}.per-base.bed.gz",
    bam_dir + "{sample}.regions.bed.gz"
  params:
    mapq='20',
    samflag='1796',
    quantize='0:1:50:150:',
    sample_name='{sample}',
    output_dir=bam_dir,
    conda = get_conda_env(config["conda_env_yaml"],"mosdepth")
  threads: get_threads(cluster_config, "mosdepth")
  singularity: singularity_image
  benchmark:
    benchmark_dir + "panel_depth_" + "{sample}.mosdepth.tsv"
  shell:
    "source activate {params.conda}; "
    "export MOSDEPTH_Q0=NO_COVERAGE;"
    "export MOSDEPTH_Q1=LOW_COVERAGE;"
    "export MOSDEPTH_Q2=CALLABLE;"
    "export MOSDEPTH_Q3=HIGH_COVERAGE;"
    "mosdepth "
      "--by {input.bed} "
      "--mapq {params.mapq} "
      "--flag {params.samflag} "
      "--quantize {params.quantize} "
      "--threads {threads} "
      "{params.output_dir}/{params.sample_name} "
      "{input.bam};"
