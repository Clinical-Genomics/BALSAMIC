# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

__author__ = "Hassan Foroughi Asl"

from BALSAMIC.tools import get_result_dir, get_conda_env

bam_dir = get_result_dir(config) + "/bam/"
resultdir = get_result_dir(config) + "/" 

rule picard_remove_duplicate:
  input:
    bam_dir + "{sample}.sorted.bam"
  output:
    rmdup = bam_dir + "{sample}.sorted.rmdup.bam"
  log:
    stats = bam_dir + "{sample}.sorted.rmdup.txt"
  params:
    conda = get_conda_env(config["conda_env_yaml"],"picard"),
  shell:
    "source activate {params.conda};"
    "java -jar -Xms8G -Xmx16G $CONDA_PREFIX/share/pica*/picard.jar "
        "MarkDuplicates "
        "INPUT={input} "
        "OUTPUT={output.rmdup} "
        "VALIDATION_STRINGENCY=SILENT "
        "MAX_FILE_HANDLES_FOR_READ_ENDS_MAP=1000 "
        "REMOVE_DUPLICATES=TRUE "
        "METRICS_FILE='{log.stats}'; "
    "samtools index {output.rmdup}; "
    "source deactivate; "

rule picard_mark_duplicate:
  input:
    bam_dir + "{sample}.sorted.bam"
  output:
    rmdup = bam_dir + "{sample}.sorted.mrkdup.bam"
  log:
    stats = bam_dir + "{sample}.sorted.mrkdup.txt"
  params:
    conda = get_conda_env(config["conda_env_yaml"],"picard"),
  shell:
    "source activate {params.conda};"
    "java -jar -Xms8G -Xmx16G $CONDA_PREFIX/share/pica*/picard.jar "
        "MarkDuplicates "
        "INPUT={input} "
        "OUTPUT={output.rmdup} "
        "VALIDATION_STRINGENCY=SILENT "
        "MAX_FILE_HANDLES_FOR_READ_ENDS_MAP=1000 "
        "REMOVE_DUPLICATES=FALSE "
        "METRICS_FILE='{log.stats}'; "
    "samtools index {output.rmdup}; "
    "source deactivate; "

rule picard_bed_to_interval:
  input:
    dict = (config["path"]["genomefa"] + config["references"]["genomefa"]).replace(".fasta",".dict"),
    bed = config["path"]["panel"] + config["bed"]["variant_panel"],
  output:
    resultdir + config["bed"]["variant_panel"] + "interval"
  params:
    conda = get_conda_env(config["conda_env_yaml"],"picard"),
  shell:
    "source activate {params.conda};"
    "picard BedToIntervalList "
      "I={input.bed} "
      "O={output} "
      "SD={input.dict}; "
    "source deactivate; "

rule picard_collectHSmetric:
  input:
    bam = bam_dir + "{sample}.sorted.bam",
    bedinterval = resultdir + config["bed"]["variant_panel"] + "interval"
  output:
    bam_dir + "{sample}.sorted.hsmetric"
  params:
    conda = get_conda_env(config["conda_env_yaml"],"picard"),
  shell:
    "source activate {params.conda};"
    "java -jar -Xms8G -Xmx16G $CONDA_PREFIX/share/pica*/picard.jar "
      "CollectHsMetrics "
      "BI={input.bedinterval} "
      "TI={input.bedinterval} "
      "I={input.bam} "
      "O={output} "
      "COVERAGE_CAP=2000 "
      "METRIC_ACCUMULATION_LEVEL=ALL_READS "
      "METRIC_ACCUMULATION_LEVEL=LIBRARY;"
    "source deactivate; "

rule picard_CollectAlignmentSummaryMetrics:
  input:
    bam = bam_dir + "{sample}.sorted.bam",
    fa = config["path"]["genomefa"] + config["references"]["genomefa"]
  output:
    bam_dir + "{sample}.sorted.alignmetric"
  params:
    conda = get_conda_env(config["conda_env_yaml"],"picard"),
    adapter = config["QC"]["adapter"]
  shell:
    "source activate {params.conda};"
    "java -jar -Xms8G -Xmx16G $CONDA_PREFIX/share/pica*/picard.jar "
      "CollectAlignmentSummaryMetrics "
      "R={input.fa} "
      "I={input.bam} "
      "O={output} "
      "ADAPTER_SEQUENCE={params.adapter} "
      "METRIC_ACCUMULATION_LEVEL=ALL_READS "
      "METRIC_ACCUMULATION_LEVEL=LIBRARY;"
    "source deactivate; "

rule picard_CollectInsertSizeMetrics:
  input:
    bam = bam_dir + "{sample}.sorted.bam"
  output:
    pdf = bam_dir + "{sample}.sorted.insertsizemetric.pdf",
    txt = bam_dir + "{sample}.sorted.insertsizemetric"
  params:
    conda = get_conda_env(config["conda_env_yaml"],"picard")
  shell:
    "source activate {params.conda};"
    "java -jar -Xms8G -Xmx16G $CONDA_PREFIX/share/pica*/picard.jar "
      "CollectInsertSizeMetrics "
      "I={input.bam} "
      "H={output.pdf} "
      "O={output.txt} "
      "M=0.01 "
      "INCLUDE_DUPLICATES=TRUE "
      "METRIC_ACCUMULATION_LEVEL=ALL_READS "
      "METRIC_ACCUMULATION_LEVEL=LIBRARY; "
    "source deactivate; "
