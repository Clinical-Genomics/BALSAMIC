# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

if 'quality_trim' in config['QC'].keys():
    fastp_param_qc = list()
    fastp_param_adapter = list()
    fastp_param_umi = list()
    if config['QC']['quality_trim']:
        fastp_param_qc.extend(["--trim_tail1", "1", "--n_base_limit", "50",
                            "--length_required", config["QC"]["min_seq_length"],
                            "--low_complexity_filter", "--trim_poly_g"])
    else:
        fastp_param_qc.extend(["--disable_quality_filtering",
                               "--disable_length_filtering",
                               "--disable_trim_poly_g"])

    if not config['QC']['adapter_trim']:
        fastp_param_adapter.extend(["--disable_adapter_trimming"])
    else:
        fastp_param_adapter.extend(["--detect_adapter_for_pe"])

    # UMI trimming will work only if adapter_trim is disabled
    if config['QC']['umi_trim']:
        fastp_param_umi.extend(["--umi","--umi_loc per_read",
                                "--umi_len", config['QC']['umi_trim_length'],
                                "--umi_prefix","UMI"])



rule fastp_umi:
    input:
        fastq_r1 = concatenated_fastq_dir + "concatenated_{sample}_R_1.fastq.gz",
        fastq_r2 = concatenated_fastq_dir + "concatenated_{sample}_R_2.fastq.gz"
    output:
        fastq_r1 = temp(concatenated_fastq_dir + "{sample}_R_1.umi_optimized.fastq.gz"),
        fastq_r2 = temp(concatenated_fastq_dir + "{sample}_R_2.umi_optimized.fastq.gz"),
        json = qc_dir + "fastp/{sample}_fastp_umi.json",
        html = qc_dir + "fastp/{sample}_fastp_umi.html",
    benchmark:
        Path(benchmark_dir, "fastp_umi" + "{sample}.tsv").as_posix()
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("fastp") + ".sif").as_posix()
    params:
        tmpdir = tmp_dir,
        qc = " ".join(fastp_param_qc),
        adapter = " ".join(fastp_param_adapter),
        sample_name = "{sample}",
    threads:
        get_threads(cluster_config, 'fastp_umi')
    message:
        "Quality control and trimming input fastq for {params.sample_name}"
    shell:
        """
export TMPDIR={params.tmpdir};

fastp \
--thread {threads} \
--in1 {input.fastq_r1} \
--in2 {input.fastq_r2} \
--out1 {output.fastq_r1} \
--out2 {output.fastq_r2} \
--json {output.json} \
--html {output.html} \
--overrepresentation_analysis \
{params.qc} \
{params.adapter};
        """
# Double pass to hard trim adapter and UMIs


rule fastp:
    input:
        fastq_r1 = concatenated_fastq_dir + "{sample}_R_1.umi_optimized.fastq.gz",
        fastq_r2 = concatenated_fastq_dir + "{sample}_R_2.umi_optimized.fastq.gz"
    output:
        fastq_r1 = temp(concatenated_fastq_dir + "{sample}_R_1.fp.fastq.gz"),
        fastq_r2 = temp(concatenated_fastq_dir + "{sample}_R_2.fp.fastq.gz"),
        json = qc_dir + "fastp/{sample}_fastp.json",
        html = qc_dir + "fastp/{sample}_fastp.html"
    benchmark:
        Path(benchmark_dir, "fastp_" + "{sample}.tsv").as_posix()
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("fastp") + ".sif").as_posix()
    params:
        tmpdir = tmp_dir,
        umi = " ".join(fastp_param_umi),
        minimum_length = config["QC"]["min_seq_length"],
        sample_name = "{sample}",
    threads:
        get_threads(cluster_config, 'fastp')
    message:
        "Quality control and trimming of umi optimized fastq file for {params.sample_name}"
    shell:
        """
export TMPDIR={params.tmpdir};

fastp \
--thread {threads} \
--in1 {input.fastq_r1} \
--in2 {input.fastq_r2} \
--out1 {output.fastq_r1} \
--out2 {output.fastq_r2} \
--json {output.json} \
--html {output.html} \
--disable_adapter_trimming \
--disable_quality_filtering \
--disable_length_filtering \
--disable_trim_poly_g \
--length_required {params.minimum_length} \
{params.umi};
        """
