# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

rule fastqc:
    """Perform quality control checks on raw sequence data."""
    input:
        fastq = input_fastq_dir + "{fastq_file_names}.fastq.gz"
    output:
        fastqccomplete = fastqc_dir + "{fastq_file_names}_fastqc.zip"
    benchmark:
        Path(benchmark_dir, "fastqc_{fastq_file_names}.tsv").as_posix()
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("fastqc") + ".sif").as_posix()
    params:
        fastqc_dir = fastqc_dir,
        fastq_file_name = "{fastq_file_names}",
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir)
    threads: get_threads(cluster_config, "fastqc")
    message:
        "Running FastQC on {params.fastq_file_name}"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
    
fastqc --threads {threads} {input.fastq} \
--dir {params.tmpdir} \
--outdir {params.fastqc_dir};
        """
