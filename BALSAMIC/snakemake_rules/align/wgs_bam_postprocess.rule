"""Rules to align, mark duplicates and realign reads with Sentieon tools."""

rule samtools_merge_bamfiles:
    input:
        bam_files = lambda wildcards: config_model.get_bam_name_per_lane(bam_dir = bam_dir, sample_name = wildcards.sample)
    output:
        bam = Path(bam_dir, "{sample_type}.{sample}.merged.bam").as_posix(),
        bai = Path(bam_dir,"{sample_type}.{sample}.merged.bam.bai").as_posix(),
    benchmark:
        Path(benchmark_dir, "samtools_merge_bamfiles_{sample_type}.{sample}.tsv").as_posix()
    singularity:
        Path(singularity_image,config["bioinfo_tools"].get("samtools") + ".sif").as_posix()
    params:
        sample_id = "{sample}"
    message:
        ("Merges all per lane bamfiles with samtools. "
         "Current sample: {params.sample_id}")
    shell:
        """
samtools merge -@ {resources.threads} {output.bam} {input.bam_files};
samtools index -@ {resources.threads} {output.bam};
        """

rule sentieon_dedup:
    input:
        bam = Path(bam_dir, "{sample_type}.{sample}.merged.bam").as_posix(),
    output:
        bam = Path(bam_dir, "{sample_type}.{sample}.dedup.bam").as_posix(),
        score = Path(bam_dir, "{sample_type}.{sample}.dedup.score").as_posix(),
        metrics = Path(qc_dir, "{sample_type}.{sample}.dedup.metrics").as_posix()
    benchmark:
        Path(benchmark_dir, "sentieon_dedup_{sample_type}.{sample}.tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sentieon_exec = config_model.sentieon.sentieon_exec,
        sentieon_lic = config_model.sentieon.sentieon_license,
        sample_id = "{sample}"
    message:
        ("Collects read information using sentieon LocusCollector and mark duplicated reads. "
         "Current sample: {params.sample_id}")
    shell:
        """
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-t {resources.threads} \
-i {input.bam} \
--algo LocusCollector \
--fun score_info {output.score} ;

{params.sentieon_exec} driver \
-t {resources.threads} \
-i {input.bam} \
--algo Dedup \
--score_info {output.score} \
--metrics {output.metrics} \
{output.bam};

rm -rf {params.tmpdir}
        """

rule sentieon_realign:
    input:
        ref = config["reference"]["reference_genome"],
        mills = config["reference"]["mills_1kg"],
        bam = Path(bam_dir, "{sample_type}.{sample}.dedup.bam").as_posix(),
        indel_1kg = config["reference"]["known_indel_1kg"]
    output:
        bam = Path(bam_dir, "{sample_type}.{sample}.dedup.realign.bam").as_posix()
    benchmark:
        Path(benchmark_dir, "sentieon_realign_{sample_type}.{sample}.tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sentieon_exec = config_model.sentieon.sentieon_exec,
        sentieon_lic = config_model.sentieon.sentieon_license,
        sample_id = "{sample}"
    message:
        "INDEL realignment using sentieon realigner for sample: {params.sample_id}"
    shell:
        """
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-r {input.ref} \
-t {resources.threads} \
-i {input.bam} \
--algo Realigner \
-k {input.mills} \
-k {input.indel_1kg} \
{output}; 

rm -rf {params.tmpdir}
        """

rule sentieon_base_calibration:
    input:
        ref = config["reference"]["reference_genome"],
        mills = config["reference"]["mills_1kg"],
        indel_1kg = config["reference"]["known_indel_1kg"],
        dbsnp = config["reference"]["dbsnp"],
        bam = lambda wildcards: config_model.get_final_bam_name(bam_dir = bam_dir, sample_type = wildcards.sample_type)
    output:
        recal_data_table = Path(bam_dir, "{sample_type}.recal_data.table").as_posix(),
        qual_recal = Path(bam_dir, "{sample_type}.recal.csv").as_posix(),
        qual_recal_plot = Path(bam_dir, "{sample_type}.recal.pdf").as_posix(),
    benchmark:
        Path(benchmark_dir, "sentieon_base_calibration_{sample_type}.tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sentieon_exec = config_model.sentieon.sentieon_exec,
        sentieon_lic = config_model.sentieon.sentieon_license,
        sample = "{sample_type}"
    message:
        "Base recalibration using Sentieon tools for {params.sample}"
    shell:
        """
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-r {input.ref} \
-t {resources.threads} \
-i {input.bam} \
--algo QualCal \
-k {input.dbsnp} \
-k {input.mills} \
-k {input.indel_1kg} {output.recal_data_table};

{params.sentieon_exec} driver \
-r {input.ref} \
-t {resources.threads} \
-i {input.bam} \
-q {output.recal_data_table} \
--algo QualCal \
-k {input.dbsnp} \
-k {input.mills} \
-k {input.indel_1kg} {output.recal_data_table}.post;

{params.sentieon_exec} driver \
-t {resources.threads} \
--algo QualCal \
--plot --before {output.recal_data_table} \
--after {output.recal_data_table}.post {output.qual_recal};

{params.sentieon_exec} plot QualCal \
-o {output.qual_recal_plot} {output.qual_recal};

rm -rf {params.tmpdir};
        """
