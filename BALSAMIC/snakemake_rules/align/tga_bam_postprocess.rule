# vim: syntax=python tabstop=4 expandtab
# coding: utf-8


rule sentieon_dedup_consensus:
    input:
        ref = config["reference"]["reference_genome"],
        bam = Path(bam_dir, "{sample_type}.{sample}.align_sort.bam").as_posix(),
    output:
        bam = Path(bam_dir, "{sample_type}.{sample}.dedup.bam").as_posix(),
        score = Path(bam_dir, "{sample_type}.{sample}.dedup.score").as_posix(),
        dedup_metrics = Path(qc_dir, "{sample_type}.{sample}.dedup.metrics").as_posix()
    benchmark:
        Path(benchmark_dir, "sentieon_dedup_{sample_type}.{sample}.tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        sample_id = "{sample}"
    threads:
        get_threads(cluster_config, 'sentieon_dedup')
    message:
        ("Collects read information using sentieon LocusCollector and collapse duplicate reads based on UMIs. "
         "Current sample: {params.sample_id}")
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-t {threads} \
-r {input.ref} \
-i {input.bam} \
--algo LocusCollector \
--umi_ecc_dist 0 \
--consensus --umi_tag XR --fun score_info {output.score} ;

{params.sentieon_exec} driver \
-t {threads} \
-r {input.ref} \
-i {input.bam} \
--algo Dedup \
--score_info {output.score} \
--metrics {output.dedup_metrics} \
{output.bam} ;
        """

# NOTE: This rule is added to fix a bug in Sentieon dedup with consensus
# SAM validation error: ERROR::INVALID_UNALIGNED_MATE_START:Record 12,
# Read name UMI-CTC-GAC-db9f21d8-A01901:175:H23G3DSX7:1:2231:18376:10739-D1,
# The unaligned mate start position is 10125, should be 0

rule samtools_fixmate:
    input:
        ref = config["reference"]["reference_genome"],
        bam = Path(bam_dir,"{sample_type}.{sample}.dedup.bam").as_posix(),
    output:
        bam = Path(bam_dir,"{sample_type}.{sample}.dedup.fixmate.bam").as_posix(),
    benchmark:
        Path(benchmark_dir,"samtools_fixmate_{sample_type}_{sample}.tsv").as_posix()
    singularity:
        Path(singularity_image,config["bioinfo_tools"].get("samtools") + ".sif").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sample_id="{sample}"
    threads:
        get_threads(cluster_config,"samtools_fixmate")
    message:
        "Running samtools collate and fixmate for: {params.sample_id}"
    shell:
        """
mkdir -p {params.tmpdir}/collate ;
mkdir -p {params.tmpdir}/fixmate ;
mkdir -p {params.tmpdir}/sort ;    

samtools collate -@ {threads} -O -u {input.bam} -T {params.tmpdir}/collate | \
samtools fixmate --reference {input.ref} -@ {threads} -T {params.tmpdir}/fixmate - - | \
samtools sort -T {params.tmpdir}/sort - -o {output} 
        """

rule cap_base_quality:
    input:
        bam = Path(bam_dir,"{sample_type}.{sample}.dedup.fixmate.bam").as_posix()
    output:
        bam = Path(bam_dir,"{sample_type}.{sample}.dedup.fixmate.qualcapped.bam").as_posix(),
    benchmark:
        Path(benchmark_dir,"cap_base_quality_{sample_type}_{sample}.tsv").as_posix()
    singularity:
        Path(singularity_image,config["bioinfo_tools"].get("pysam") + ".sif").as_posix()
    params:
        sample_id = "{sample}",
        cap_base_qualities = get_script_path("cap_base_quality_in_bam.py")
    threads:
        get_threads(cluster_config, "cap_base_quality")
    message:
        "Capping base qualities to 70 in bamfile using pysam in sample: {params.sample_id}"
    shell:
        """
python {params.cap_base_qualities} --max_quality 70 {input.bam} {output.bam} ; 
samtools index -@ {threads} {output.bam}
        """
