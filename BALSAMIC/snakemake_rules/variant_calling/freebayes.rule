# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

__author__ = "Hassan Foroughi Asl"

from BALSAMIC.tools import get_conda_env, get_picard_mrkdup


var_caller = 'freebayes'
freebayes_dir = var_caller + "/"

chromlist = get_chrom(config["path"]["panel"] + config["bed"]["capture_kit"])
picarddup = get_picard_mrkdup(config)

rule freebayes_germline:
  input:
    fa = config["path"]["genomefa"] + config["references"]["genomefa"],
    bam = bam_dir + "{sample}.sorted." + picarddup  + ".bam",
    bed = vcf_dir + "split_bed/" + "{bedchrom}" + "." + config["bed"]["capture_kit"],
  output:
    vcf_dir + freebayes_dir + "split_vcf/" + "{sample}.{bedchrom}_" + config["vcf"][var_caller]["default"]
  params:
    conda = get_conda_env(config["conda_env_yaml"],"freebayes"),
  threads: 4
  shell:
    "source activate {params.conda}; "
    "freebayes --bam {input.bam} --fasta-reference {input.fa} --targets {input.bed} "
        "--use-duplicate-reads --min-mapping-quality 30 --min-base-quality 20 --min-alternate-fraction 0.01 "
        "--genotype-qualities --use-mapping-quality | bgzip > {output}; "
    "source deactivate; "

rule freebayes_merge:
  input:
    expand(vcf_dir + freebayes_dir + "split_vcf/{{sample}}.{chrom}_" + config["vcf"][var_caller]["default"], chrom=chromlist)
  output:
    vcf_dir + config["vcf"][var_caller]["type"] + "." + config["vcf"][var_caller]["mutation"] + "." + "{sample}" + "." + var_caller + ".vcf.gz"
  params:
    conda = get_conda_env(config["conda_env_yaml"], "freebayes"),
  shell:
    "source activate {params.conda}; "
    "bcftools concat {input} | bcftools sort - | bgzip > {output}; "
    "tabix -f -p vcf {output}; "
    "source deactivate;"
