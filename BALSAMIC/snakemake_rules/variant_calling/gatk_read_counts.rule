# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

rule gatk_collectreadcounts:
    input:
        bam = lambda wildcards: config_model.get_final_bam_name(bam_dir = bam_dir, sample_name = wildcards.sample),
        genome_interval = config["reference"]["genome_interval"]
    output:
        readcounts_hdf5 = cnv_dir + "{sample}.collectreadcounts.hdf5"
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sample = "{sample}"
    benchmark:
        Path(benchmark_dir, 'gatk_collectreadcounts_' + "{sample}.tsv").as_posix()
    singularity:
        Path(singularity_image,config["bioinfo_tools"].get("gatk") + ".sif").as_posix()
    threads:
        get_threads(cluster_config, 'gatk_collectreadcounts')
    message:
        "Running GATK CollectReadCounts on {params.sample} for GENS."
    shell:
      """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};  
    
gatk --java-options "-XX:-UseLargePages -Xmx20g" CollectReadCounts -I {input.bam} \
-L {input.genome_interval} \
--tmp-dir {params.tmpdir} \
--interval-merging-rule OVERLAPPING_ONLY \
-O {output.readcounts_hdf5}

rm -rf {params.tmpdir}
      """

