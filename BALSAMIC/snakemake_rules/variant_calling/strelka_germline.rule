# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

__author__ = "Hassan Foroughi Asl"

from BALSAMIC.tools import get_result_dir, get_conda_env, get_picard_mrkdup

bam_dir = get_result_dir(config) + "/bam/"
vcf_dir = get_result_dir(config) + "/vcf/"

var_caller = "strelka_germline"
strelka_dir = vcf_dir + var_caller + "_{sample}/"
picarddup = get_picard_mrkdup(config)

rule strelka_germline:
  input:
    fa = config["path"]["genomefa"] + config["references"]["genomefa"],
    bam = bam_dir + "{sample}.sorted." + picarddup  + ".bam",
    mantaindel = vcf_dir + "manta_germline_{sample}/results/variants/candidateSmallIndels.vcf.gz"
  output:
    final = vcf_dir + config["vcf"][var_caller]["type"] + "." + config["vcf"][var_caller]["mutation"] + "." + "{sample}" + "." + var_caller + ".vcf.gz"
  params:
    tmpdir = strelka_dir,
    runmode = "local",
    conda = get_conda_env(config["conda_env_yaml"],"strelka"),
  threads: 4
  shell:
    "source activate {params.conda};"
    "rm -rf {params.tmpdir}; "
    "configureStrelkaGermlineWorkflow.py "
        "--bam={input.bam} "
        "--referenceFasta={input.fa} "
        "--indelCandidates {input.mantaindel} "
        "--outputCallableRegions "
        "--exome "
        "--runDir={params.tmpdir}; "
    "python {params.tmpdir}/runWorkflow.py -m {params.runmode} -j {threads}; "
    "cp {params.tmpdir}/results/variants/variants.vcf.gz {output.final}; " 
    "tabix -p vcf -f {output.final}; "
    "source deactivate; "
