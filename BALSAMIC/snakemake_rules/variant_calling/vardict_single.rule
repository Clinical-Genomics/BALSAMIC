# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

__author__ = "Hassan Foroughi Asl"

from BALSAMIC.tools import get_result_dir, get_conda_env, get_chrom

bam_dir = get_result_dir(config) + "/bam/"
vcf_dir = get_result_dir(config) + "/vcf/"

vardict_dir = "vardict/"

chromlist = get_chrom(config["path"]["panel"] + config["bed"]["capture_kit"])

rule vardict_single:
  input:
    fa = config["path"]["genomefa"] + config["references"]["genomefa"],
    bamT = bam_dir + "tumor.merged.bam",
    bed = vcf_dir + "split_bed/" + "{bedchrom}" + "." + config["bed"]["capture_kit"],
  output:
    vcf_dir + vardict_dir + "split_vcf/" + "{bedchrom}_" + config["vcf"]["vardict"]["default"]
  params:
    af = "0.01",
    max_pval = "0.9",
    max_mm = "4.5",
    col_info = "-c 1 -S 2 -E 3 -g 4",
    name = config["analysis"]["sample_id"],
    conda = get_conda_env(config["conda_env_yaml"],"vardict"),
  shell:
    "source activate {params.conda}; "
    "vardict -G {input.fa} -f {params.af} -N {params.name} "
        "-b {input.bamT} "
        "{params.col_info} {input.bed} "
        "| teststrandbias.R "
        "| var2vcf_valid.pl -P {params.max_pval} "
        "-m {params.max_mm} -E -f {params.af} -N {params.name} "
        "| bgzip > {output}; "
    "tabix -p vcf {output}; "
    "source deactivate;"

rule vardict_merge_chrom:
  input:
    expand(vcf_dir + vardict_dir + "split_vcf/{chrom}_" + config["vcf"]["vardict"]["default"], chrom=chromlist) 
  output:
    vcf_dir + config["vcf"]["vardict"]["type"] + "/" + config["vcf"]["vardict"]["merged"]
  params:
    conda = get_conda_env(config["conda_env_yaml"],"vardict"),
  shell:
    "source activate {params.conda} ; "
    "bcftools concat {input} | bcftools sort - | bgzip > {output}; "
    "tabix -f -p vcf {output}; "
    "source deactivate;" 
