# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

# Variant-calling using TNscope and VarDict


rule sentieon_tnscope_tga_tumor_normal:
    input:
        bamN = config_model.get_final_bam_name(bam_dir = bam_dir, sample_name = normal_sample),
        bamT = config_model.get_final_bam_name(bam_dir = bam_dir, sample_name = tumor_sample),
        ref_fa = config["reference"]["reference_genome"],
        bed = config["panel"]["capture_kit"],
        dbsnp = config["reference"]["dbsnp"]
    output:
        vcf_tnscope = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
        namemap_snv = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
    benchmark:
        Path(benchmark_dir, "sentieon_tnscope_umi_" + config["analysis"]["case_id"] + ".tsv").as_posix()
    resources:
        mem_mb=lambda wc, input: min(max(2.1 * input.size_mb, 5000), 250000) # Cap at 250GB. Scale memory with input size (min 5GB) to handle large TGA cases without over-allocating for typical inputs (e.g. myeloid). 2.1 factor chosen after testing
    params:
        algo = params.tnscope_tga.algo,
        error_rate = params.tnscope_tga.error_rate,
        init_tumor_lod = params.tnscope_tga.init_tumorLOD,
        padding = params.tnscope_tga.padding,
        pcr_model = params.tnscope_tga.pcr_model,
        prune_factor = params.tnscope_tga.prunefactor,
        tumor_af = params.tnscope_tga.filter_tumor_af,
        tumor_lod = params.tnscope_tga.min_tumorLOD,
        case_name= config["analysis"]["case_id"],
        disable_detect= params.tnscope_tga.disable_detect,
        tmpdir= tempfile.mkdtemp(prefix=tmp_dir),
        tumor= "TUMOR",
        normal= "NORMAL",
        sentieon_exec=config_model.sentieon.sentieon_exec,
        sentieon_lic=config_model.sentieon.sentieon_license,
        housekeeper_id= {"id": config["analysis"]["case_id"],"tags": "research"}
    message:
        "Calling single nucleotide variants using TNscope for {params.case_name}"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-t {threads} \
-r {input.ref_fa} \
-i {input.bamT} \
-i {input.bamN} \
--interval {input.bed} \
--interval_padding {params.padding} \
--algo {params.algo} \
--disable_detector {params.disable_detect} \
--tumor_sample {params.tumor} \
--normal_sample {params.normal} \
--dbsnp {input.dbsnp} \
--trim_soft_clip \
--min_tumor_allele_frac {params.tumor_af} \
--filter_t_alt_frac {params.tumor_af} \
--min_init_tumor_lod {params.init_tumor_lod} \
--min_tumor_lod {params.tumor_lod} \
--max_error_per_read {params.error_rate} \
--pcr_indel_model {params.pcr_model} \
--prune_factor {params.prune_factor} \
{output.vcf_tnscope};

echo -e \"{params.tumor}\\tTUMOR\\n{params.normal}\\tNORMAL\" > {output.namemap_snv};
        """

rule vardict_tumor_normal:
    input:
        fa = config["reference"]["reference_genome"],
        bamN = config_model.get_final_bam_name(bam_dir = bam_dir, sample_name = normal_sample),
        bamT = config_model.get_final_bam_name(bam_dir = bam_dir, sample_name = tumor_sample),
        bed = vcf_dir + "pad_bedfile/" + "100bp_padding_" + capture_kit
    output:
        vcf = vcf_dir + "vardict/vardict.vcf"
    benchmark:
        Path(benchmark_dir,'vardict_tumor_normal.tsv').as_posix()
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("vardict") + ".sif").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        af = params.vardict.allelic_frequency,
        max_pval = params.vardict.max_pval,
        max_mm = params.vardict.max_mm,
        col_info = params.vardict.column_info,
        case_name = config["analysis"]["case_id"],
    message:
        "Calling variants using vardict for {params.case_name}"
    shell:
        """
export TMPDIR={params.tmpdir};
export _JAVA_OPTIONS="-Djava.io.tmpdir={params.tmpdir} -Xss256k"; 
export VAR_DICT_OPTS='\"-Xms10G\" \"-Xmx25G\" \"-XX:+UseG1GC\" \"-XX:MaxHeapFreeRatio=50\"'; 

vardict-java -I 600 -G {input.fa} -f {params.af} -N {params.case_name} \
-b \"{input.bamT}|{input.bamN}\" \
-th {threads} \
{params.col_info} {input.bed} \
| testsomatic.R \
| var2vcf_paired.pl -P {params.max_pval} \
-m {params.max_mm} -M -f {params.af} -N {params.case_name} > {output.vcf};

rm -rf {params.tmpdir};
    """

rule post_process_vardict:
    input:
        vcf = vcf_dir + "vardict/vardict.sorted.vcf"
    output:
        vcf_vardict = vcf_dir + "vardict/SNV.somatic." + config["analysis"]["case_id"] + ".vardict.vcf.gz",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".vardict.sample_name_map"
    params:
        tmpdir=tempfile.mkdtemp(prefix=tmp_dir),
        case_name=config["analysis"]["case_id"],
        edit_vcf_script= get_script_path("edit_vcf_info.py"),
        variant_caller= "vardict"
    benchmark:
        Path(benchmark_dir,'post_process_vardict_' + config["analysis"]["case_id"] + ".tsv").as_posix()
    singularity:
        Path(singularity_image,config["bioinfo_tools"].get("bcftools") + ".sif").as_posix()
    message:
        ("Bgzip, index and reheader merged VarDict vcf for case: {params.case_name}")
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};

python {params.edit_vcf_script} \
--input_vcf {input.vcf} \
--output_vcf {params.tmpdir}/vardict_edit.vcf \
--variant_caller {params.variant_caller};

bgzip {params.tmpdir}/vardict_edit.vcf \ ; 
tabix -f -p vcf {params.tmpdir}/vardict_edit.vcf.gz ;

echo \"TUMOR\" > {params.tmpdir}/reheader ;
echo \"NORMAL\" >> {params.tmpdir}/reheader ;

bcftools reheader -s {params.tmpdir}/reheader {params.tmpdir}/vardict_edit.vcf.gz -o {output.vcf_vardict} ;
tabix -f -p vcf {output.vcf_vardict} ;

echo -e \"tTUMOR\\tTUMOR\\nNORMAL\\tNORMAL\" > {output.namemap} ; 
echo -e \"tTUMOR\" > {output.namemap}.tumor ;
echo -e \"nNORMAL\" > {output.namemap}.normal ; 

rm -rf {params.tmpdir} ;
    """

rule gatk_update_vcf_sequence_dictionary:
    input:
        ref = config["reference"]["reference_genome"],
        vcf = vcf_dir + "vardict/SNV.somatic." + config["analysis"]["case_id"] + ".vardict.vcf.gz",
    output:
        vcf_vardict = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".vardict.vcf.gz",
    params:
        housekeeper_id = {"id": config["analysis"]["case_id"], "tags": "research"},
        tmpdir=tempfile.mkdtemp(prefix=tmp_dir),
    benchmark:
        Path(benchmark_dir,"gatk_update_vcf_sequence_dictionary" + config["analysis"]["case_id"] + ".tsv").as_posix()
    singularity:
        Path(singularity_image,config["bioinfo_tools"].get("gatk") + ".sif").as_posix()
    message:
        "Running GATK UpdateVCFSequenceDictionary on VarDict VCF."
    shell:
        """
export TMPDIR={params.tmpdir};  

ref=$(echo {input.ref} | sed 's/.fasta/.dict/g') ;

gatk UpdateVCFSequenceDictionary -V {input.vcf} --source-dictionary $ref --replace --output {output.vcf_vardict} ;

rm -rf {params.tmpdir}
        """
