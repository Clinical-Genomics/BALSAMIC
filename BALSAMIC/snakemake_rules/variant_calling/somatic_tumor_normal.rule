# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

rule vardict_tumor_normal:
    input:
        fa = config["reference"]["reference_genome"],
        bamN = bam_dir + "normal.merged.bam",
        bamT = bam_dir + "tumor.merged.bam",
        bed = vcf_dir + "split_bed/{bedchrom}." + capture_kit,
    output:
        temp(vcf_dir + "vardict/split_vcf/{bedchrom}_vardict.vcf.gz")
    benchmark:
        Path(benchmark_dir,'vardict_tumor_normal_' + "{bedchrom}.tsv").as_posix()
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("vardict") + ".sif").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        af = params.vardict.allelic_frequency,
        max_pval = params.vardict.max_pval,
        max_mm = params.vardict.max_mm,
        col_info = params.vardict.column_info,
        case_name = config["analysis"]["case_id"],
    threads:
        get_threads(cluster_config, "vardict_tumor_normal")
    message:
        "Calling variants using vardict for sample {params.case_name}"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export VAR_DICT_OPTS='\"-Djava.io.tmpdir={params.tmpdir}\" \"-Xmx32G\"';

vardict-java -U -u -I 600 -G {input.fa} -f {params.af} -N {params.case_name} \
-b \"{input.bamT}|{input.bamN}\" \
-th {threads} \
{params.col_info} {input.bed} \
| testsomatic.R \
| var2vcf_paired.pl -P {params.max_pval} \
-m {params.max_mm} -M -f {params.af} -N {params.case_name} \
| bgzip > {output};

tabix -p vcf {output};
rm -rf {params.tmpdir};
    """

rule vardict_merge:
    input:
        expand(vcf_dir + "vardict/split_vcf/{chrom}_vardict.vcf.gz", chrom=chromlist)
    output:
        vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".vardict.vcf.gz",
        yaml = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".vardict.yaml",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".vardict.sample_name_map"
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        case_name = config["analysis"]["case_id"],
    benchmark:
        Path(benchmark_dir,'vardict_merge_' + config["analysis"]["case_id"] + ".tsv").as_posix()
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("vardict") + ".sif").as_posix()
    threads:
        get_threads(cluster_config,"vardict_merge")
    message:
        ("Merging all chromosomes vardict results into "
        "single vcf using bcftools for sample {params.case_name}")
    shell:
        """
mkdir -p {params.tmpdir};

bcftools concat {input} | bcftools sort --temp-dir {params.tmpdir} - | bgzip > {output.vcf}; 
tabix -f -p vcf {output.vcf};

echo -e \"{params.case_name}\\tTUMOR\\n{params.case_name}-match\\tNORMAL\" > {output.namemap}; 
echo -e \"{params.case_name}\" > {output.namemap}.tumor;
echo -e \"{params.case_name}-match\" > {output.namemap}.normal; 
echo '{{ vcf: {{ vardict: {{ name: vardict, path: {output.vcf} }} }} }}' > {output.yaml};

rm -rf {params.tmpdir};
    """

rule sentieon_TNhaplotyper:
    input:
        bamT = bam_dir + "tumor.merged.bam",
        bamN = bam_dir + "normal.merged.bam",
        interval = config["panel"]["capture_kit"],
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
    output:
        vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.vcf.gz",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.sample_name_map",
    benchmark:
        Path(benchmark_dir + 'sentieon_TNhaplotyper_' + config["analysis"]["case_id"] + ".tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        tumor = "TUMOR",
        normal = "NORMAL",
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        case_name = config["analysis"]["case_id"]
    threads:
        get_threads(cluster_config, 'sentieon_TNhaplotyper')
    message:
        "Calling variants using TNhaplotyper for sample {params.case_name}"
    shell:
        """
mkdir -p {params.tmpdir}; 
export TMPDIR={params.tmpdir}; 
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-r {input.ref} \
-t {threads} \
-i {input.bamT} \
-i {input.bamN} \
--interval {input.interval} \
--algo TNhaplotyper \
--tumor_sample {params.tumor} \
--normal_sample {params.normal} \
--dbsnp {input.dbsnp} {output.vcf};
        
echo -e \"{params.tumor}\\tTUMOR\\n{params.normal}\\tNORMAL\" > {output.namemap}; 
rm -rf {params.tmpdir};
        """
