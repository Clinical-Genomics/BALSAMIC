# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

def get_pon(config):
    """ return pon cli string, complete with file """
    if "PON" in config["analysis"]:
        return os.path.abspth(config["analysis"]["PON"])
    else:
        return None


rule sentieon_TNsnv_tumor_only:
    input:
        bam = expand(bam_dir + "{mysample}.dedup.realign.bam", mysample=get_sample_type(config["samples"], "tumor")),
        recal_data_table = expand(bam_dir + "{mysample}.dedup.realign.recal_data.table", mysample=get_sample_type(config["samples"], "tumor")),
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
        cosmic = config["reference"]["cosmic"],
    output:
        vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnsnv.vcf.gz",
        stats = vcf_dir + config["analysis"]["case_id"] + ".tnsnv.call_stats",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnsnv.sample_name_map",
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        tumor = get_sample_type(config["samples"], "tumor"),
        pon = " " if get_pon(config) is None else " ".join(["--pon", get_pon(config)]),
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
    threads: get_threads(cluster_config, 'sentieon_TNsnv_tumor_only')
    log:
        vcf_dir + config["analysis"]["case_id"] + ".tnsnv.log"
    benchmark:
        benchmark_dir + config["analysis"]["case_id"] + ".tnsnv.tsv"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver -r {input.ref} -t {threads} -q {input.recal_data_table} -i {input.bam} --algo TNsnv --tumor_sample {params.tumor} {params.pon} --cosmic {input.cosmic} --dbsnp {input.dbsnp} --call_stats_out {output.stats} {output.vcf} 

echo -e \"{params.tumor}\\tTUMOR\" > {output.namemap}; 
rm -rf {params.tmpdir};
        """

rule sentieon_TNhaplotyper_tumor_only:
    input:
        bam = expand(bam_dir + "{mysample}.dedup.realign.bam", mysample=get_sample_type(config["samples"], "tumor")),
        recal_data_table = expand(bam_dir + "{mysample}.dedup.realign.recal_data.table", mysample=get_sample_type(config["samples"], "tumor")),
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
        cosmic = config["reference"]["cosmic"],
    output:
        vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.vcf.gz",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.sample_name_map",
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        tumor = get_sample_type(config["samples"], "tumor"),
        pon = " " if get_pon(config) is None else " ".join(["--pon", get_pon(config)]),
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
    threads: get_threads(cluster_config, 'sentieon_TNhaplotyper_tumor_only')
    log:
        vcf_dir + config["analysis"]["case_id"] + ".tnsnv.log"
    benchmark:
        benchmark_dir + config["analysis"]["case_id"] + ".tnhaplotyper.tsv"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver -r {input.ref} -t {threads} -i {input.bam} -q {input.recal_data_table} --algo TNhaplotyper --tumor_sample {params.tumor} {params.pon} --cosmic {input.cosmic} --dbsnp {input.dbsnp} {output.vcf} 

echo -e \"{params.tumor}\\tTUMOR\" > {output.namemap}; 
rm -rf {params.tmpdir};
        """


rule sentieon_TNscope_tumor_only:
    input:
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
        bam = expand(bam_dir + "{tumor}.dedup.realign.bam", tumor=get_sample_type(config["samples"], "tumor")),
        recal = expand(bam_dir + "{tumor}.dedup.realign.recal_data.table", tumor=get_sample_type(config["samples"], "tumor")),
    output:
        vcf = vcf_dir + "sentieon_tnscope/ALL.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
        namemap_snv = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
        namemap_sv = vcf_dir + "SV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        tumor = get_sample_type(config["samples"], "tumor"),
        pon = " " if get_pon(config) is None else " ".join(["--pon", get_pon(config)]),
        sentieon_ml_tnscope = config["SENTIEON_TNSCOPE"],
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
    threads: get_threads(cluster_config, 'sentieon_TNscope_tumor_only')
    log: 
        vcf_dir + config["analysis"]["case_id"] + ".tnscope_tumor_only.log"
    benchmark:
        benchmark_dir + config["analysis"]["case_id"] + ".tnscope.tsv"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver -t {threads} -r {input.ref} -i {input.bam} -q {input.recal} --algo TNscope --tumor_sample {params.tumor} {params.pon} --dbsnp {input.dbsnp} {output.vcf};

echo -e \"{params.tumor}\\tTUMOR\" > {output.namemap_snv};
cp {output.namemap_snv} {output.namemap_sv}
rm -rf {params.tmpdir};
        """

rule bcftools_view_split_variant:
    input:
        ref = config["reference"]["reference_genome"],
        vcf = vcf_dir + "sentieon_tnscope/ALL.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
    output:
        vcf_snv = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
        vcf_sv = vcf_dir + "SV.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
    params:
        conda = config["bioinfo_tools"].get("bcftools"),
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
    threads:
        get_threads(cluster_config, 'split_sentieon_sv_snv')
    singularity:
        Path(singularity_image, config["bioinfo_tools"].get("bcftools") + ".sif").as_posix() 
    benchmark:
        benchmark_dir + 'split_sentieon_sv_snv_' + config["analysis"]["case_id"] + ".tsv"
    shell:
        """
source activate {params.conda};
export TMPDIR={params.tmpdir};
mkdir -p {params.tmpdir};

bcftools view --include 'INFO/SVTYPE=="."' -O z -o {output.vcf_snv} {input.vcf}; 
tabix -p vcf -f {output.vcf_snv};

bcftools view --include 'INFO/SVTYPE!="."' -O z -o {output.vcf_sv} {input.vcf}; 
tabix -p vcf -f {output.vcf_sv};
        """
