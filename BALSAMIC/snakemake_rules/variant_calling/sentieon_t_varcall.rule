# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

from BALSAMIC.utils.constants import VARCALL_PARAMS

def get_pon(config):
    """ return pon cli string, complete with file """
    if "PON" in config["analysis"]:
        return os.path.abspth(config["analysis"]["PON"])
    else:
        return None

rule sentieon_base_calibration:
    input:
        ref = config["reference"]["reference_genome"],
        mills = config["reference"]["mills_1kg"],
        indel_1kg = config["reference"]["1kg_known_indel"],
        dbsnp = config["reference"]["dbsnp"],
        bam = Path(bam_dir, "{sample_type}.merged.bam").as_posix()
    output:
        recal_data_table = Path(bam_dir, "{sample_type}.merged.recal_data.table").as_posix(),
        qual_recal = Path(bam_dir, "{sample_type}.merged.recal.csv").as_posix(),
        qual_recal_plot = Path(bam_dir, "{sample_type}.recal.pdf").as_posix(),
    benchmark:
        Path(benchmark_dir, "sentieon_base_calibration_{sample_type}.tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        sample_id = "sample_type"
    threads:
        get_threads(cluster_config, 'sentieon_base_calibration')
    message:
        "Base recalibration using sentieon tools for sample {params.sample_id}"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver --algo QualCal \
-r {input.ref} \
-t {threads} \
-i {input.bam} \
-k {input.dbsnp} \
-k {input.mills} \
-k {input.indel_1kg} \
{output.recal_data_table};

{params.sentieon_exec} driver --algo QualCal \
-r {input.ref} \
-t {threads} \
-i {input.bam} \
-q {output.recal_data_table} \
-k {input.dbsnp} \
-k {input.mills} \
-k {input.indel_1kg} \
{output.recal_data_table}.post;

{params.sentieon_exec} driver --algo QualCal \
-t {threads} \
--plot --before {output.recal_data_table} \
--after {output.recal_data_table}.post \
{output.qual_recal};

{params.sentieon_exec} plot QualCal \ 
-o {output.qual_recal_plot} {output.qual_recal};

rm -rf {params.tmpdir};
        """

rule sentieon_TNhaplotyper_tumor_only:
    input:
        bam = expand(bam_dir + "tumor.merged.bam"),
        recal_data_table = expand(bam_dir + "tumor.merged.recal_data.table"),
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
        cosmic = config["reference"]["cosmic"],
    output:
        vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.vcf.gz",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.sample_name_map",
    benchmark:
        Path(benchmark_dir, "sentieon_TNhaplotyper_tumor_only_" + config["analysis"]["case_id"] + ".tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        tumor = "TUMOR", 
        pon = " " if get_pon(config) is None else " ".join(["--pon", get_pon(config)]),
        pcr_model = "NONE",
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        sample_id = config["analysis"]["case_id"]
    threads:
        get_threads(cluster_config, 'sentieon_TNhaplotyper_tumor_only')
    message:
        "Calling SNVs using sentieon TNhaplotyper for sample {params.sample_id}"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver --algo TNhaplotyper \
-r {input.ref} \
-t {threads} \
-i {input.bam} \
-q {input.recal_data_table} \
--tumor_sample {params.tumor} {params.pon} \
--pcr_indel_mode {params.pcr_model} \
--cosmic {input.cosmic} \
--dbsnp {input.dbsnp} {output.vcf};

echo -e \"{params.tumor}\\tTUMOR\" > {output.namemap}; 
rm -rf {params.tmpdir};
        """


rule sentieon_TNscope_tumor_only:
    input:
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
        bam = expand(bam_dir + "tumor.merged.bam"),
        recal = expand(bam_dir + "tumor.merged.recal_data.table")
    output:
        vcf = vcf_dir + "sentieon_tnscope" + "/" + "ALL.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
        namemap_snv = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
        namemap_sv = vcf_dir + "SV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
    benchmark:
        Path(benchmark_dir, "sentieon_TNscope_tumor_only_" + config[ "analysis" ][ "case_id" ] + ".tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        tumor = "TUMOR", 
        tumor_options = VARCALL_PARAMS["tnscope"]["tumor"], 
        pon = " " if get_pon(config) is None else " ".join(["--pon", get_pon(config)]),
        pcr_model = "NONE",
        sentieon_ml_tnscope = config["SENTIEON_TNSCOPE"],
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        sample_id =  config["analysis"]["case_id"]
    threads:
        get_threads(cluster_config, 'sentieon_TNscope_tumor_only')
    message:
        "Calling SNVs using sentieon TNscope for sample {params.sample_id}"
    shell:
        """
mkdir -p {params.tmpdir};
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver --algo TNscope \
-t {threads} \
-r {input.ref} \
-i {input.bam} \
-q {input.recal} \
--tumor_sample {params.tumor} {params.pon} \
--dbsnp {input.dbsnp} \
--pcr_indel_mode {params.pcr_model} \
{params.tumor_options} {output.vcf};

echo -e \"{params.tumor}\\tTUMOR\" > {output.namemap_snv};
cp {output.namemap_snv} {output.namemap_sv}
rm -rf {params.tmpdir};
        """
