# vim: syntax=python tabstop=4 expandtab
# coding: utf-8



rule sentieon_base_calibration:
    input:
        ref = config["reference"]["reference_genome"],
        mills = config["reference"]["mills_1kg"],
        indel_1kg = config["reference"]["known_indel_1kg"],
        dbsnp = config["reference"]["dbsnp"],
        bam = lambda wildcards: config_model.get_final_bam_name(bam_dir = bam_dir, sample_type = wildcards.sample_type)
    output:
        recal_data_table = Path(bam_dir, "{sample_type}.recal_data.table").as_posix(),
        qual_recal = Path(bam_dir, "{sample_type}.recal.csv").as_posix(),
        qual_recal_plot = Path(bam_dir, "{sample_type}.recal.pdf").as_posix(),
    benchmark:
        Path(benchmark_dir, "sentieon_base_calibration_{sample_type}.tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        sample = "{sample_type}"
    threads:
        get_threads(cluster_config, 'sentieon_base_calibration')
    message:
        "Base recalibration using Sentieon tools for {params.sample}"
    shell:
        """
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-r {input.ref} \
-t {threads} \
-i {input.bam} \
--algo QualCal \
-k {input.dbsnp} \
-k {input.mills} \
-k {input.indel_1kg} {output.recal_data_table};

{params.sentieon_exec} driver \
-r {input.ref} \
-t {threads} \
-i {input.bam} \
-q {output.recal_data_table} \
--algo QualCal \
-k {input.dbsnp} \
-k {input.mills} \
-k {input.indel_1kg} {output.recal_data_table}.post;

{params.sentieon_exec} driver \
-t {threads} \
--algo QualCal \
--plot --before {output.recal_data_table} \
--after {output.recal_data_table}.post {output.qual_recal};

{params.sentieon_exec} plot QualCal \
-o {output.qual_recal_plot} {output.qual_recal};

rm -rf {params.tmpdir};
        """


rule sentieon_TNscope:
    input:
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
        cosmic = config["reference"]["cosmic"],
        bamT = config_model.get_final_bam_name(bam_dir = bam_dir, sample_name = tumor_sample),
        bamN = config_model.get_final_bam_name(bam_dir = bam_dir, sample_name = normal_sample),
        recalT = expand(bam_dir + "tumor.recal_data.table"),
        recalN = expand(bam_dir + "normal.recal_data.table"),
    output:
        vcf_tnscope = vcf_dir + "sentieon_tnscope/ALL.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
        namemap_snv = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
        namemap_sv = vcf_dir + "SV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
    benchmark:
        Path(benchmark_dir, 'sentieon_TNscope_' + config[ "analysis" ][ "case_id" ] + ".tsv").as_posix()
    params:
        tmpdir = tempfile.mkdtemp(prefix=tmp_dir),
        tumor = "TUMOR",
        normal = "NORMAL",
        pcr_model = params.common.pcr_model,
        tumor_options = VARCALL_PARAMS["tnscope"]["tumor"],
        normal_options = VARCALL_PARAMS["tnscope"]["normal"],
        sentieon_exec = config["SENTIEON_EXEC"],
        sentieon_lic = config["SENTIEON_LICENSE"],
        case_name = config["analysis"]["case_id"]
    threads:
        get_threads(cluster_config, 'sentieon_TNscope')
    message:
        ("Calling SNVs and SVs using Sentieon TNscope and "
         "applying machine learning algorithm for sample {params.case_name}")
    shell:
        """
export TMPDIR={params.tmpdir};
export SENTIEON_TMPDIR={params.tmpdir};
export SENTIEON_LICENSE={params.sentieon_lic};

{params.sentieon_exec} driver \
-t {threads} \
-r {input.ref} \
-i {input.bamT} \
-q {input.recalT} \
-i {input.bamN} \
-q {input.recalN} \
--algo TNscope \
--tumor_sample {params.tumor} \
--normal_sample {params.normal} \
--dbsnp {input.dbsnp} \
--cosmic {input.cosmic} \
--pcr_indel_mode {params.pcr_model} \
{params.tumor_options} \
{params.normal_options} {output.vcf_tnscope};

echo -e \"{params.tumor}\\tTUMOR\\n{params.normal}\\tNORMAL\" > {output.namemap_snv};
cp {output.namemap_snv} {output.namemap_sv}
rm -rf {params.tmpdir};
        """
