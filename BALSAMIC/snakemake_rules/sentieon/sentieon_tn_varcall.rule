# vim: syntax=python tabstop=4 expandtab
# coding: utf-8


from BALSAMIC.utils.rule import get_conda_env
from BALSAMIC.utils.rule import get_sample_type
from BALSAMIC.utils.rule import get_threads


rule sentieon_TN_corealign:
    input:
        ref = config["reference"]["reference_genome"],
        bamT = expand(bam_dir + "{mysample}.dedup.realign.bam", mysample=get_sample_type(config["samples"], "tumor")),
        bamN = expand(bam_dir + "{mysample}.dedup.realign.bam", mysample=get_sample_type(config["samples"], "normal")),
        recalT = expand(bam_dir + "{mysample}.dedup.realign.recal_data.table", mysample=get_sample_type(config["samples"], "tumor")),
        recalN = expand(bam_dir + "{mysample}.dedup.realign.recal_data.table", mysample=get_sample_type(config["samples"], "normal")),
        mills = config["reference"]["mills_1kg"],
        indel_1kg = config["reference"]["1kg_known_indel"],
    output:
        bam = bam_dir + config["analysis"]["case_id"] + ".corealign.bam"
    params:
        tmpdir = bam_dir,
        sentieon_exec = SENTIEON_INSTALL_DIR + "/bin/sentieon",
        sentieon_lic = SENTIEON_LICENSE, 
    threads: get_threads(cluster_config, 'sentieon_TN_corealign')
    log:
        vcf_dir + config["analysis"]["case_id"] + ".corealign.log"
    benchmark:
        benchmark_dir + 'sentieon_TN_corealign_' + config["analysis"]["case_id"] + ".corealign.tsv"
    shell:
        """
export SENTIEON_LICENSE={params.sentieon_lic};

rand_str=$(openssl rand -hex 5);
export SENTIEON_TMPDIR={params.tmpdir}/${{rand_str}};

{params.sentieon_exec} driver -r {input.ref} -t {threads} -i {input.bamT} -i {input.bamN} -q {input.recalT} -q {input.recalN} --algo Realigner -k {input.mills} -k {input.indel_1kg} {output.bam}

        """


rule sentieon_TNsnv:
    input:
        bam = bam_dir + config["analysis"]["case_id"] + ".corealign.bam",
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
    output:
        vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnsnv.vcf.gz",
        stats = vcf_dir + config["analysis"]["case_id"] + ".tnsnv.call_stats",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnsnv.sample_name_map",
    params:
        tmpdir = bam_dir,
        tumor = get_sample_type(config["samples"], "tumor"),
        normal = get_sample_type(config["samples"], "normal"),
        sentieon_exec = SENTIEON_INSTALL_DIR + "/bin/sentieon",
        sentieon_lic = SENTIEON_LICENSE, 
    threads: get_threads(cluster_config, 'sentieon_TNsnv')
    log:
        vcf_dir + config["analysis"]["case_id"] + ".tnsnv.log"
    benchmark:
        benchmark_dir + 'sentieon_TNsnv_' + config["analysis"]["case_id"] + ".tnsnv.tsv"
    shell:
        """
export SENTIEON_LICENSE={params.sentieon_lic};

rand_str=$(openssl rand -hex 5);
export SENTIEON_TMPDIR={params.tmpdir}/${{rand_str}};

{params.sentieon_exec} driver -r {input.ref} -t {threads} -i {input.bam} --algo TNsnv --tumor_sample {params.tumor} --normal_sample {params.normal} --dbsnp {input.dbsnp} --call_stats_out {output.stats} {output.vcf} 

echo -e \"{params.tumor}\\tTUMOR\n{params.normal}\\tNORMAL\" > {output.namemap};
        """


rule sentieon_TNhaplotyper:
    input:
        bam = bam_dir + config["analysis"]["case_id"] + ".corealign.bam",
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
    output:
        vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.vcf.gz",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnhaplotyper.sample_name_map",
    params:
        tmpdir = bam_dir,
        tumor = get_sample_type(config["samples"], "tumor"),
        normal = get_sample_type(config["samples"], "normal"),
        sentieon_exec = SENTIEON_INSTALL_DIR + "/bin/sentieon",
        sentieon_lic = SENTIEON_LICENSE, 
    threads: get_threads(cluster_config, 'sentieon_TNhaplotyper')
    log:
        vcf_dir + config["analysis"]["case_id"] + ".tnhaplotyper.log"
    benchmark:
        benchmark_dir + 'sentieon_TNhaplotyper_' + config["analysis"]["case_id"] + ".tnhaplotyper.tsv"
    shell:
        """
export SENTIEON_LICENSE={params.sentieon_lic};

rand_str=$(openssl rand -hex 5);
export SENTIEON_TMPDIR={params.tmpdir}/${{rand_str}};

{params.sentieon_exec} driver -r {input.ref} -t {threads} -i {input.bam} --algo TNhaplotyper --tumor_sample {params.tumor} --normal_sample {params.normal} --dbsnp {input.dbsnp} {output.vcf}

echo -e \"{params.tumor}\\tTUMOR\n{params.normal}\\tNORMAL\" > {output.namemap};
        """


rule sentieon_TNscope:
    input:
        ref = config["reference"]["reference_genome"],
        dbsnp = config["reference"]["dbsnp"],
        bamT = expand(bam_dir + "{tumor}.dedup.realign.bam", tumor=get_sample_type(config["samples"], "tumor")),
        bamN = expand(bam_dir + "{normal}.dedup.realign.bam", normal=get_sample_type(config["samples"], "normal")),
        recalT = expand(bam_dir + "{tumor}.dedup.realign.recal_data.table", tumor=get_sample_type(config["samples"], "tumor")),
        recalN = expand(bam_dir + "{normal}.dedup.realign.recal_data.table", normal=get_sample_type(config["samples"], "normal")),
    output:
        vcf = vcf_dir + "sentieon_tnscope/SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
        namemap = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.sample_name_map",
    params:
        tmpdir = bam_dir,
        tumor = get_sample_type(config["samples"], "tumor"),
        normal = get_sample_type(config["samples"], "normal"),
        sentieon_exec = SENTIEON_INSTALL_DIR + "/bin/sentieon",
        sentieon_lic = SENTIEON_LICENSE, 
        variant_setting = "--min_init_normal_lod 0.5 --min_normal_lod 1.0 --min_init_tumor_lod 1.0 --min_tumor_lod 8"
    threads: get_threads(cluster_config, 'sentieon_TNscope')
    log: 
        vcf_dir + config["analysis"]["case_id"] + ".tnscope.log"
    benchmark:
        benchmark_dir + 'sentieon_TNscope_' + config["analysis"]["case_id"] + ".tnscope.tsv"
    shell:
        """
export SENTIEON_LICENSE={params.sentieon_lic};

rand_str=$(openssl rand -hex 5);
export SENTIEON_TMPDIR={params.tmpdir}/${{rand_str}};

{params.sentieon_exec} driver -t {threads} -r {input.ref} -i {input.bamT} -q {input.recalT} -i {input.bamN} -q {input.recalN} --algo TNscope --tumor_sample {params.tumor} --normal_sample {params.normal} --dbsnp {input.dbsnp} {params.variant_setting} {output.vcf}

echo -e \"{params.tumor}\\tTUMOR\n{params.normal}\\tNORMAL\" > {output.namemap};
        """


rule sentioen_filter_TNscope:
    input:
        ref = config["reference"]["reference_genome"],
        tnscope_vcf = vcf_dir + "sentieon_tnscope/SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
    output:
        tnscope_filtered_vcf = vcf_dir + "SNV.somatic." + config["analysis"]["case_id"] + ".tnscope.vcf.gz",
    params:
        tmpdir = bam_dir,
        sentieon_exec = SENTIEON_INSTALL_DIR + "/bin/sentieon",
        sentieon_lic = SENTIEON_LICENSE, 
        sentieon_ml_tnscope = SENTIEON_TNSCOPE
    threads: get_threads(cluster_config, 'sentieon_filter_TNscope')
    log:
        vcf_dir + config["analysis"]["case_id"] + ".tnscope.filtered.log",
    benchmark:
        benchmark_dir + 'sentioen_filter_TNscope_' + config["analysis"]["case_id"] + ".tnscope_filter.tsv"
    shell:
        """
export SENTIEON_LICENSE={params.sentieon_lic};
export SENTIEON_TNSCOPE={params.sentieon_ml_tnscope}

rand_str=$(openssl rand -hex 5);
export SENTIEON_TMPDIR={params.tmpdir}/${{rand_str}};

{params.sentieon_exec} driver -r {input.ref} --algo TNModelApply -m {params.sentieon_ml_tnscope} -v {input.tnscope_vcf} {output.tnscope_filtered_vcf}
        """

