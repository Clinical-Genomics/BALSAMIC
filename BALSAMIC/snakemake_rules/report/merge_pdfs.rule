"""Rules for merging PDF reports."""


rule merge_cnv_pdf_reports:
    """Rule for merging CNV PDF reports."""
    input:
        report_paths=cnv_report_paths,
    output:
        cnv_report_pdf=f"{cnv_dir}CNV.somatic.{config['analysis']['case_id']}.report.pdf",
    params:
        housekeeper_id={"id": config["analysis"]["case_id"], "tags": "clinical"},
        merge_pdfs_script=get_script_path("merge_pdfs.py"),
        loh_regions_pdf=f"{cnv_dir}CNV.somatic.{config['analysis']['case_id']}.purecn.LOHregions.csv.pdf",
        loh_genes_pdf=f"{cnv_dir}CNV.somatic.{config['analysis']['case_id']}.purecn.LOHgenes.csv.pdf",
    message:
        "Merging CNV PDF reports {output.cnv_report_pdf}"
    benchmark:
        Path(benchmark_dir, f"merge_cnv_pdf_reports_{config['analysis']['case_id']}.tsv").as_posix()
    shell:
        """
IFS=" " read -r -a report_paths <<< "{input.report_paths}"
if [[ -f "{params.loh_regions_pdf}" ]] && [[ -f "{params.loh_genes_pdf}" ]]; then
    report_paths+=("{params.loh_regions_pdf}" "{params.loh_genes_pdf}")
fi
python "{params.merge_pdfs_script}" "${{report_paths[@]}}" "{output.cnv_report_pdf}"
        """
