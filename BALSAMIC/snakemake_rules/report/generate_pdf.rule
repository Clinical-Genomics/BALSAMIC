"""Rules for generating TGA PDF reports."""


rule csv_to_pdf:
    """Converting LOH report CSV files to PDF format."""
    input:
        purity_csv=f"{cnv_dir}CNV.somatic.{config['analysis']['case_id']}.purecn.purity.csv",
    output:
        purity_csv_pdf=f"{cnv_dir}CNV.somatic.{config['analysis']['case_id']}.purecn.purity.csv.pdf",
    params:
        csv_to_pdf_script=get_script_path("csv_to_pdf.py"),
        loh_regions=f"{cnv_dir}CNV.somatic.{config['analysis']['case_id']}.purecn.LOHregions.csv",
        loh_genes=f"{cnv_dir}CNV.somatic.{config['analysis']['case_id']}.purecn.LOHgenes.csv",
    threads: 1
    resources:
        runtime=60,
        mem_mb=5000
    message:
        "Converting CSV files to PDF"
    shell:
        """
for file in "{input.purity_csv}" "{params.loh_regions}" "{params.loh_genes}"; do
    if [[ -f "$file" ]]; then
        python "{params.csv_to_pdf_script}" "$file" "$file.pdf" --header
    fi
done
        """

rule txt_to_pdf:
    """Converting AscatNgs statistics TXT file to PDF format."""
    input:
        sample_statistics=f"{vcf_dir}CNV.somatic.{config['analysis']['case_id']}.ascat.samplestatistics.txt",
    output:
        sample_statistics_pdf=f"{vcf_dir}CNV.somatic.{config['analysis']['case_id']}.ascat.samplestatistics.txt.pdf",
    params:
        csv_to_pdf_script=get_script_path("csv_to_pdf.py"),
    threads: 1
    resources:
        runtime=60,
        mem_mb=5000
    message:
        "Converting AscatNgs statistics TXT file to PDF"
    shell:
        """
python "{params.csv_to_pdf_script}" "{input.sample_statistics}" "{output.sample_statistics_pdf}" --delimiter " "
        """


rule msi_to_pdf:
    """Converting MSI TXT file to PDF format."""
    input:
        msi_result= f"{vcf_dir}MSI.somatic.{config['analysis']['case_id']}.msisensorpro.msi",
    output:
        msi_result_pdf= f"{vcf_dir}MSI.somatic.{config['analysis']['case_id']}.msisensorpro.msi.pdf",
    params:
        csv_to_pdf_script=get_script_path("csv_to_pdf.py"),
    threads: 1
    resources:
        runtime=60,
        mem_mb=5000
    message:
        "Converting MSI TXT file to PDF"
    shell:
        """
python "{params.csv_to_pdf_script}" "{input.msi_result}" "{output.msi_result_pdf}" --delimiter "\t" --header
        """

rule image_to_pdf:
    """Converting CNV PNG files to PDF format."""
    input:
        plot=f"{vcf_dir}{{plot}}"
    output:
        plot_pdf=f"{vcf_dir}{{plot}}.{FileType.PDF}"
    wildcard_constraints:
        plot="|".join(config_model.get_cnv_report_plots()),
    params:
        image_to_pdf_script=get_script_path("image_to_pdf.py"),
    threads: 1
    resources:
        runtime=60,
        mem_mb=5000
    message:
        "Converting CNV PNG files to PDF"
    shell:
        """
python "{params.image_to_pdf_script}" "{input.plot}" "{output.plot_pdf}"
        """
