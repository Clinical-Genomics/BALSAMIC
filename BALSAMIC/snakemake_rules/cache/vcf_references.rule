# vim: syntax=python tabstop=4 expandtab
# coding: utf-8


wildcard_constraints:
    vcf="|".join(cache_config.get_reference_paths_by_file_type(file_type=FileType.VCF)),


rule compress_vcfs:
    """Compress VCF reference files."""
    input:
        singularity_image=f"{config['containers_dir']}/{config['bioinfo_tools']['bgzip']}.{FileType.SIF}",
        vcf="{vcf}",
    output:
        vcf_gz=f"{{vcf}}.{FileType.GZ}",
    singularity:
        f"{config['containers_dir']}/{config['bioinfo_tools']['bgzip']}.{FileType.SIF}"
    threads: get_threads(cluster_config=cluster_config, rule_name="compress_vcfs")
    message:
        "Compressing VCF variant file {input.vcf}"
    benchmark:
        f"{{vcf}}.{FileType.GZ}.benchmark.{FileType.TSV}"
    log:
        f"{{vcf}}.{FileType.GZ}.{FileType.LOG}",
    shell:
        """
        bgzip "{input.vcf}" &> "{log}"
        """


rule index_vcfs:
    """Index VCF reference files."""
    input:
        singularity_image=f"{config['containers_dir']}/{config['bioinfo_tools']['tabix']}.{FileType.SIF}",
        vcf_gz=f"{{vcf}}.{FileType.GZ}",
    output:
        vcf_gz_tbi=f"{{vcf}}.{FileType.GZ}.{FileType.TBI}",
    singularity:
        f"{config['containers_dir']}/{config['bioinfo_tools']['tabix']}.{FileType.SIF}"
    threads: get_threads(cluster_config=cluster_config, rule_name="index_vcfs")
    message:
        "Indexing VCF variant file {input.vcf_gz}"
    benchmark:
        f"{{vcf}}.{FileType.GZ}.{FileType.TBI}.{FileType.TSV}"
    log:
        f"{{vcf}}.{FileType.GZ}.{FileType.TBI}.{FileType.LOG}",
    shell:
        """
        tabix -p vcf "{input.vcf_gz}" &> "{log}"
        """
