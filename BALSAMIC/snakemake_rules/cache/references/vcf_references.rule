# vim: syntax=python tabstop=4 expandtab
# coding: utf-8


wildcard_constraints:
    vcf="|".join(
        cache_config.get_reference_paths_by_file_type_and_compression(
            file_type=FileType.VCF, compression=True
        )
    ),


rule compress_vcfs:
    """Compress VCF reference files."""
    input:
        singularity_image=f"{config['containers_dir']}/{config['bioinfo_tools']['bgzip']}.sif",
        vcf="{vcf}",
    output:
        vcf_gz="{vcf}.gz",
    singularity:
        "{input.singularity_image}"
    threads: get_threads(cluster_config, "compress_vcfs")
    message:
        "Compressing VCF variant file {input.vcf}"
    log:
        "{vcf}.gz.log",
    shell:
        """
        bgzip "{input.vcf}" &> "{log}"
        """


rule index_vcfs:
    """Index VCF reference files."""
    input:
        singularity_image=f"{config['containers_dir']}/{config['bioinfo_tools']['tabix']}.sif",
        vcf_gz="{vcf}.gz",
    output:
        vcf_gz_tbi="{vcf}.gz.tbi",
    singularity:
        "{input.singularity_image}"
    threads: get_threads(cluster_config, "index_vcfs")
    message:
        "Indexing VCF variant file {input.vcf_gz}"
    log:
        "{vcf}.gz.tbi.log",
    shell:
        """
        tabix -p vcf "{input.vcf_gz}" &> "{log}"
        """
