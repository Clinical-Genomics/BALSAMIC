"""Rules to download Docker containers."""


rule download_containers:
    """Download Singularity containers from Docker Hub."""
    output:
        container_path=f"{config['containers_dir']}/{{singularity_image}}.sif",
    wildcard_constraints:
        singularity_image="|".join(cache_config.containers.keys()),
    params:
        tmp_dir=f"{config['containers_dir']}/tmp",
        singularity_image="{singularity_image}",
        dockerhub_image=lambda wildcards: config["containers"][
            wildcards.singularity_image
        ],
    threads: get_threads(cluster_config=cluster_config, rule_name="download_containers")
    message:
        "Downloading singularity image {output.container_path}"
    benchmark:
        f"{config['containers_dir']}/benchmarks/{{singularity_image}}.{FileType.SIF}.benchmark.{FileType.TSV}"
    log:
        f"{config['containers_dir']}/logs/{{singularity_image}}.{FileType.SIF}.{FileType.LOG}",
    shell:
        """
        export SINGULARITY_CACHEDIR={params.tmp_dir}/{params.singularity_image}
        singularity pull {output.container_path} {params.dockerhub_image} &> "{log}"
        """
